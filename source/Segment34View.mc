import Toybox.Application;
import Toybox.Graphics;
import Toybox.Lang;
import Toybox.System;
import Toybox.WatchUi;
import Toybox.Time;
import Toybox.Weather;
import Toybox.Complications;
using Toybox.Position;

(:background_excluded)
class Segment34View extends WatchUi.WatchFace {

    hidden var visible as Boolean = true;
    hidden var screenHeight as Number;
    hidden var screenWidth as Number;
    (:initialized) hidden var clockHeight as Number;
    (:initialized) hidden var clockWidth as Number;
    (:initialized) hidden var labelHeight as Number;
    (:initialized) hidden var labelMargin as Number;
    (:initialized) hidden var tinyDataHeight as Number;
    (:initialized) hidden var smallDataHeight as Number;
    (:initialized) hidden var largeDataHeight as Number;
    (:initialized) hidden var largeDataWidth as Number;
    (:initialized) hidden var bottomDataWidth as Number;
    (:initialized) hidden var baseX as Number;
    (:initialized) hidden var baseY as Number;
    hidden var centerX as Number;
    hidden var centerY as Number;
    hidden var marginX as Number;
    hidden var marginY as Number;
    hidden var halfMarginY as Number = 0;
    hidden var halfClockHeight as Number = 0;
    hidden var halfClockWidth as Number = 0;
    hidden var aboveLine2Adjustment as Number = 0;
    hidden var barBottomAdj as Number = 0;
    hidden var bottomFiveAdj as Number = 0;
    hidden var fieldSpaceingAdj as Number = 0;
    hidden var textSideAdj as Number = 0;
    hidden var secondsClipWidth as Number = 24;
    hidden var iconYAdj as Number = 3;
    hidden var histogramBarWidth as Number = 2;
    hidden var histogramBarSpacing as Number = 2;
    hidden var histogramHeight as Number = 20;
    hidden var histogramTargetWidth as Number = 40;
    hidden var propHistogramSize as Number = 0;
    hidden var bottomFieldWidths as Array<Number> = [3, 3, 3, 0];

    hidden var fontMoon as WatchUi.FontResource?;
    hidden var fontIcons as WatchUi.FontResource;
    hidden var fontClock as WatchUi.FontResource?;
    hidden var fontClockOutline as WatchUi.FontResource?;
    hidden var fontLabel as WatchUi.FontResource?;
    hidden var fontTinyData as WatchUi.FontResource?;
    hidden var fontSmallData as WatchUi.FontResource?;
    hidden var fontAODData as WatchUi.FontResource?;
    hidden var fontBottomData as WatchUi.FontResource?;
    hidden var fontBattery as WatchUi.FontResource?;
    hidden var cachedDayOfWeek as Number = -1;
    hidden var cachedDayName as String = "";
    hidden var cachedMonth as Number = -1;
    hidden var cachedMonthName as String = "";

    // Layout Caching
    hidden var fieldXCoords as Array<Number> = [0, 0, 0, 0];
    hidden var fieldY as Number = 0;
    hidden var bottomFiveY as Number = 0;
    (:Square) hidden var bottomFive1X as Number = 0;
    (:Square) hidden var bottomFive2X as Number = 0;
    (:Square) hidden var dualBottomFieldActive as Boolean = false;
    (:Square) hidden var bottomFiveYOriginal as Number = 0;

    hidden var drawGradient as BitmapResource?;
    hidden var drawAODPattern as BitmapResource?;
    
    public var infoMessage as String = "";
    public var nightModeOverride as Number = -1;
    hidden var themeColors as Array<Graphics.ColorType> = [];
    hidden var nightMode as Boolean?;
    hidden var weatherCondition as CurrentConditions or StoredWeather or Null;
    hidden var propWeatherProvider as Number = 0;
    hidden var owmError as String or Null = null;
    hidden var hrHistoryData as Array<Number>?;
    hidden var canBurnIn as Boolean = false;
    hidden var isSleeping as Boolean = false;
    hidden var lastUpdate as Number? = null;
    hidden var lastSlowUpdate as Number? = null;
    hidden var cachedValues as Dictionary = {};
    hidden var cachedTempUnit as String = "C";
    hidden var cachedRunDist7Days as Number = 0;
    hidden var cachedBikeDist7Days as Number = 0;
    hidden var cachedSwimDist7Days as Number = 0;
    hidden var lastActivityDistUpdate as Number = 0;

    hidden var lastHfTime as Number? = null;
    hidden var lastCcHash as Number? = null;
    hidden var isLowMem as Boolean = false;

    hidden var doesPartialUpdate as Boolean = false;
    // CGM Connect Widget complication IDs
    hidden var cgmComplicationId as Complications.Id? = null;
    hidden var cgmAgeComplicationId as Complications.Id? = null;
    
    hidden var propIs24H as Boolean = false;
    hidden var propTheme as Integer = 0;
    hidden var propNightTheme as Integer = -1;
    hidden var propNightThemeActivation as Number = 0;
    hidden var propColorOverride as String = "";
    hidden var propColorOverride2 as String = "";
    hidden var propClockOutlineStyle as Number = 0;
    hidden var propClockGradientOverlay as Number = 0;
    hidden var propClockFont as Number = 0;
    hidden var propFontSize as Number = 0;
    hidden var propBatteryVariant as Number = 3;
    hidden var propShowSeconds as Boolean = true;
    hidden var propFieldLayout as Number = 0;
    hidden var propLeftValueShows as Number = 6;
    hidden var propMiddleValueShows as Number = 10;
    hidden var propRightValueShows as Number = 0;
    hidden var propFourthValueShows as Number = 0;
    hidden var propAlwaysShowSeconds as Boolean = false;
    hidden var propUpdateFreq as Number = 5;
    hidden var propShowClockBg as Boolean = true;
    hidden var propShowDataBg as Boolean = false;
    hidden var propAodStyle as Number = 1;
    hidden var propAodFieldShows as Number = -1;
    hidden var propAodRightFieldShows as Number = -2;
    hidden var propDateFieldShows as Number = -1;
    hidden var propBottomFieldShows as Number = 17;
    hidden var propBottomField2Shows as Number = -2;
    hidden var propAodAlignment as Number = 0;
    hidden var propDateAlignment as Number = 0;
    hidden var propBottomFieldAlignment as Number = 2;
    hidden var propBottomFieldLabelAlignment as Number = 0;
    hidden var propLeftBarShows as Number = 1;
    hidden var propRightBarShows as Number = 2;
    hidden var propIcon1 as Number = 1;
    hidden var propIcon2 as Number = 2;
    hidden var propHemisphere as Number = 0;
    hidden var propHourFormat as Number = 0;
    hidden var propZeropadHour as Boolean = true;
    hidden var propTimeSeparator as Number = 0;
    hidden var propTempUnit as Number = 0;
    hidden var propShowTempUnit as Boolean = true;
    hidden var propDistanceUnit as Number = 0;
    hidden var propIsMetricDistance as Boolean = true;
    hidden var propWindUnit as Number = 0;
    hidden var propPressureUnit as Number = 0;
    hidden var propTopPartShows as Number = 0;
    hidden var propHistogramData as Number = 0;
    hidden var propSunriseFieldShows as Number = 39;
    hidden var propSunsetFieldShows as Number = 40;
    hidden var propWeatherLine1Shows as Number = 79;
    hidden var propWeatherLine2Shows as Number = 80;
    hidden var propWeatherFormat1 as String = "t w p";
    hidden var propWeatherFormat2 as String = "c";
    hidden var propDateFormat as Number = 0;
    hidden var propDateCustomFormat as String = "DDD, DD MMMM";
    hidden var propNotificationCountShows as Number = 36;
    hidden var propSecondsShows as Number = -3;
    hidden var propTzOffset1 as Number = 0;
    hidden var propTzOffset2 as Number = 0;
    hidden var propTzName1 as String = "";
    hidden var propTzName2 as String = "";
    hidden var propWeekOffset as Number = 0;
    hidden var propLabelVisibility as Number = 0;
    hidden var propSmallFontVariant as Number = 0;
    hidden var propBottomFontVariant as Number = 2;
    hidden var propStressDynamicColor as Boolean = false;

    // Cached Labels
    hidden var strLabelTopLeft as String = "";
    hidden var strLabelTopRight as String = "";
    hidden var strLabelBottomLeft as String = "";
    hidden var strLabelBottomMiddle as String = "";
    hidden var strLabelBottomRight as String = "";
    hidden var strLabelBottomFourth as String = "";

    const battFull = "|||||||||||||||||||||||||||||||||||";
    const battEmpty = "{{{{{{{{{{{{{{{{{{{{{{{{{{{{{{{{{{{";

    enum colorNames {
        bg = 0,
        clock,
        clockBg,
        outline,
        dataVal,
        fieldBg,
        fieldLbl,
        date,
        dateDim,
        notif,
        stress,
        bodybatt,
        moon,
        lowBatt
    }

    var clockBgText = "";

    (:Round260) const barWidth = 3;
    (:Round280) const barWidth = 3;
    (:Round390) const barWidth = 4;
    (:Round416) const barWidth = 4;
    (:Round454) const barWidth = 4;
    (:Square) const barWidth = 4;

    function initialize() {
        WatchFace.initialize();

        canBurnIn = System.getDeviceSettings().requiresBurnInProtection;

        screenHeight = Toybox.System.getDeviceSettings().screenHeight;
        screenWidth = Toybox.System.getDeviceSettings().screenWidth;
        fontIcons = Application.loadResource(Rez.Fonts.icons);
        centerX = Math.round(screenWidth / 2);
        centerY = Math.round(screenHeight / 2);
        marginY = Math.round(screenHeight / 30);
        marginX = Math.round(screenWidth / 20);
        reloadSettings();
    }

    hidden function reloadSettings() as Void {
        updateProperties();

        loadResources();
        if(propTopPartShows == 0) {
            fontMoon = Application.loadResource(Rez.Fonts.moon);
        }

        halfClockHeight = Math.round(clockHeight / 2);
        if(clockBgText.length() == 4) {
            halfClockWidth = Math.round((clockWidth / 5 * 4.2) / 2);
        } else {
            halfClockWidth = Math.round(clockWidth / 2);
        }

        halfMarginY = Math.round(marginY / 2);

        calculateLayout();

        updateWeather();
    }

    hidden function updateActiveLabels() as Void {
        var fieldWidths = getFieldWidths();
        strLabelTopLeft = getLabelByType(propSunriseFieldShows, 1);
        strLabelTopRight = getLabelByType(propSunsetFieldShows, 1);
        if(propFontSize == 0) {
            strLabelBottomLeft = getLabelByType(propLeftValueShows, fieldWidths[0] - 1);
            strLabelBottomMiddle = getLabelByType(propMiddleValueShows, fieldWidths[1] - 1);
            strLabelBottomRight = getLabelByType(propRightValueShows, fieldWidths[2] - 1);
            strLabelBottomFourth = getLabelByType(propFourthValueShows, fieldWidths[3] - 1);
        } else { // Large text
            strLabelBottomLeft = getLabelByType(propLeftValueShows, 1);
            strLabelBottomMiddle = getLabelByType(propMiddleValueShows, 1);
            strLabelBottomRight = getLabelByType(propRightValueShows, 1);
            strLabelBottomFourth = getLabelByType(propFourthValueShows, 1);
        }
    }

    hidden function loadFontVariant(resDefault, resReadable, resLines, variant as Number) as FontResource {
        var selectedRes = resLines;
        if (variant == 0) {
            selectedRes = resDefault;
        } else if (variant == 1) {
            selectedRes = resReadable;
        }
        return Application.loadResource(selectedRes) as FontResource;
    }

    hidden function loadAODGraphics() as Void {
        if(propClockGradientOverlay == 0 or propClockGradientOverlay == 2 or propClockGradientOverlay == 4) {
            drawGradient = Application.loadResource(Rez.Drawables.gradient) as BitmapResource;
        } else {
            drawGradient = null;
        }

        if(propClockGradientOverlay == 0 or propClockGradientOverlay == 1) {
            drawAODPattern = Application.loadResource(Rez.Drawables.aod) as BitmapResource;
        } else if(propClockGradientOverlay == 4 or propClockGradientOverlay == 5) {
            drawAODPattern = Application.loadResource(Rez.Drawables.aod3) as BitmapResource;
        } else {
            drawAODPattern = Application.loadResource(Rez.Drawables.aod2) as BitmapResource;
        }
    }

    (:Round260)
    hidden function loadResources() as Void {
        switch(propClockFont) {
            case 1:  fontClock = Application.loadResource(Rez.Fonts.segments80_2); break;
            case 2:  fontClock = Application.loadResource(Rez.Fonts.segments80_3); break;
            case 3:  fontClock = Application.loadResource(Rez.Fonts.segments80_4); break;
            case 4:  fontClock = Application.loadResource(Rez.Fonts.segments80_2r); break;
            default: fontClock = Application.loadResource(Rez.Fonts.segments80); break;
        }

        if(propFontSize == 0) {
            fontTinyData = Application.loadResource(Rez.Fonts.storre);
            fontSmallData = loadFontVariant(Rez.Fonts.led_small, Rez.Fonts.led_small_readable, Rez.Fonts.led_small_lines, propSmallFontVariant);
            fontBottomData = loadFontVariant(Rez.Fonts.led, Rez.Fonts.led_inbetween, Rez.Fonts.led_lines, propBottomFontVariant);
            fontLabel = Application.loadResource(Rez.Fonts.smol);
            fontBattery = fontLabel;

            marginY = 5;
            labelHeight = 8;
            tinyDataHeight = 10;
            smallDataHeight = 13;
            secondsClipWidth = 24;
            bottomFiveAdj = 2;
            baseY = centerY - smallDataHeight;
            aboveLine2Adjustment = 5;
            bottomFieldWidths = [3, 3, 3, 0];
        } else {
            fontTinyData = Application.loadResource(Rez.Fonts.storre);
            fontSmallData = loadFontVariant(Rez.Fonts.led, Rez.Fonts.led_inbetween, Rez.Fonts.led_lines, propSmallFontVariant);
            fontBottomData = loadFontVariant(Rez.Fonts.led, Rez.Fonts.led_inbetween, Rez.Fonts.led_lines, propBottomFontVariant);
            fontLabel = fontTinyData;
            fontAODData = Application.loadResource(Rez.Fonts.led);
            fontBattery = Application.loadResource(Rez.Fonts.led_small_lines);

            marginY = 4;
            labelHeight = 10;
            tinyDataHeight = 10;
            smallDataHeight = 20;
            secondsClipWidth = 36;
            bottomFiveAdj = 2;
            baseY = centerY - 6;
            aboveLine2Adjustment = 3;
            bottomFieldWidths = [4, 4, 0, 0];
        }

        clockHeight = 80;
        clockWidth = 230;
        labelMargin = 6;
        largeDataHeight = 20;
        largeDataWidth = 18;
        bottomDataWidth = 18;

        baseX = centerX + 1;
        fieldSpaceingAdj = 5;
        barBottomAdj = 1;
        histogramBarWidth = (propHistogramSize == 1) ? 2 : 1;
        histogramBarSpacing = (propHistogramSize == 1) ? 2 : 1;
        histogramHeight = (propHistogramSize == 1) ? 25 : 18;
        histogramTargetWidth = (propHistogramSize == 1) ? 25 : 40;
    }

    (:Round280)
    hidden function loadResources() as Void {
        switch(propClockFont) {
            case 1:  fontClock = Application.loadResource(Rez.Fonts.segments80wide_2); break;
            case 2:  fontClock = Application.loadResource(Rez.Fonts.segments80wide_3); break;
            case 3:  fontClock = Application.loadResource(Rez.Fonts.segments80wide_4); break;
            case 4:  fontClock = Application.loadResource(Rez.Fonts.segments80wide_2r); break;
            default: fontClock = Application.loadResource(Rez.Fonts.segments80wide); break;
        }
        if(propFontSize == 0) {
            fontTinyData = Application.loadResource(Rez.Fonts.storre);
            fontSmallData = loadFontVariant(Rez.Fonts.led_small, Rez.Fonts.led_small_readable, Rez.Fonts.led_small_lines, propSmallFontVariant);
            fontBottomData = loadFontVariant(Rez.Fonts.led, Rez.Fonts.led_inbetween, Rez.Fonts.led_lines, propBottomFontVariant);
            fontLabel = Application.loadResource(Rez.Fonts.smol);
            fontBattery = fontLabel;

            marginY = 8;
            labelHeight = 8;
            smallDataHeight = 13;
            secondsClipWidth = 24;
            bottomFiveAdj = 5;
            baseY = centerY - smallDataHeight - 4;
            aboveLine2Adjustment = 2;
            bottomFieldWidths = [4, 3, 4, 0];
        } else {
            fontTinyData = Application.loadResource(Rez.Fonts.storre);
            fontSmallData = loadFontVariant(Rez.Fonts.led, Rez.Fonts.led_inbetween, Rez.Fonts.led_lines, propSmallFontVariant);
            fontBottomData = loadFontVariant(Rez.Fonts.led, Rez.Fonts.led_inbetween, Rez.Fonts.led_lines, propBottomFontVariant);
            fontLabel = fontTinyData;
            fontAODData = Application.loadResource(Rez.Fonts.led);
            fontBattery = Application.loadResource(Rez.Fonts.led_small_lines);

            marginY = 6;
            labelHeight = 10;
            smallDataHeight = 20;
            secondsClipWidth = 36;
            bottomFiveAdj = 5;
            baseY = centerY - 5;
            aboveLine2Adjustment = 2;
            bottomFieldWidths = [3, 3, 3, 0];
        }

        clockHeight = 80;
        clockWidth = 240;
        labelMargin = 6;
        tinyDataHeight = 10;
        largeDataHeight = 20;
        largeDataWidth = 18;
        bottomDataWidth = 18;
        baseX = centerX;
        barBottomAdj = 1;
        histogramBarWidth = (propHistogramSize == 1) ? 2 : 1;
        histogramBarSpacing = (propHistogramSize == 1) ? 2 : 1;
        histogramHeight = (propHistogramSize == 1) ? 28 : 20;
        histogramTargetWidth = (propHistogramSize == 1) ? 25 : 40;
    }

    (:Round390)
    hidden function loadResources() as Void {
        switch(propClockFont) {
            case 1:  fontClock = Application.loadResource(Rez.Fonts.segments125_2); fontClockOutline = Application.loadResource(Rez.Fonts.segments125outline_2); break;
            case 2:  fontClock = Application.loadResource(Rez.Fonts.segments125_3); fontClockOutline = Application.loadResource(Rez.Fonts.segments125outline_3); break;
            case 3:  fontClock = Application.loadResource(Rez.Fonts.segments125_4); fontClockOutline = Application.loadResource(Rez.Fonts.segments125outline_4); break;
            case 4:  fontClock = Application.loadResource(Rez.Fonts.segments125_2r); fontClockOutline = Application.loadResource(Rez.Fonts.segments125outline_2r); break;
            default: fontClock = Application.loadResource(Rez.Fonts.segments125);   fontClockOutline = Application.loadResource(Rez.Fonts.segments125outline); break;
        }

        if(propFontSize == 0) {
            fontTinyData = Application.loadResource(Rez.Fonts.led_small_lines);
            fontSmallData = loadFontVariant(Rez.Fonts.led, Rez.Fonts.led_inbetween, Rez.Fonts.led_lines, propSmallFontVariant);
            fontBottomData = loadFontVariant(Rez.Fonts.led_big, Rez.Fonts.led_big_readable, Rez.Fonts.led_big_lines, propBottomFontVariant);
            fontLabel = Application.loadResource(Rez.Fonts.storre);
            fontAODData = Application.loadResource(Rez.Fonts.led);
            fontBattery = fontTinyData;

            clockWidth = 360;
            textSideAdj = 2;
            marginY = 10;
            labelHeight = 10;
            smallDataHeight = 20;
            bottomFiveAdj = 6;
            baseY = centerY - smallDataHeight - 3;
            bottomFieldWidths = [4, 3, 4, 0];
        } else {
            fontTinyData = Application.loadResource(Rez.Fonts.led_small_lines);
            fontSmallData = loadFontVariant(Rez.Fonts.led_big, Rez.Fonts.led_big_readable, Rez.Fonts.led_big_lines, propSmallFontVariant);
            fontBottomData = loadFontVariant(Rez.Fonts.led_big, Rez.Fonts.led_big_readable, Rez.Fonts.led_big_lines, propBottomFontVariant);
            fontLabel = fontTinyData;
            fontAODData = Application.loadResource(Rez.Fonts.led_big);
            fontBattery = Application.loadResource(Rez.Fonts.led_lines);

            clockWidth = 360 - 4;
            textSideAdj = 10;
            marginY = 8;
            labelHeight = 13;
            smallDataHeight = 27;
            bottomFiveAdj = 4;
            baseY = centerY - 6;
            bottomFieldWidths = [3, 3, 3, 0];
        }

        loadAODGraphics();

        clockHeight = 125;
        labelMargin = 8;
        tinyDataHeight = 13;
        largeDataHeight = 27;
        largeDataWidth = 24;
        bottomDataWidth = 24;
        baseX = centerX;
        barBottomAdj = 2;
        histogramHeight = (propHistogramSize == 1) ? 35 : 25;
    }

    (:Round416)
    hidden function loadResources() as Void {
        switch(propClockFont) {
            case 1:  fontClock = Application.loadResource(Rez.Fonts.segments125_2); fontClockOutline = Application.loadResource(Rez.Fonts.segments125outline_2); break;
            case 2:  fontClock = Application.loadResource(Rez.Fonts.segments125_3); fontClockOutline = Application.loadResource(Rez.Fonts.segments125outline_3); break;
            case 3:  fontClock = Application.loadResource(Rez.Fonts.segments125_4); fontClockOutline = Application.loadResource(Rez.Fonts.segments125outline_4); break;
            case 4:  fontClock = Application.loadResource(Rez.Fonts.segments125_2r); fontClockOutline = Application.loadResource(Rez.Fonts.segments125outline_2r); break;
            default: fontClock = Application.loadResource(Rez.Fonts.segments125);   fontClockOutline = Application.loadResource(Rez.Fonts.segments125outline); break;
        }

        if(propFontSize == 0) {
            fontTinyData = Application.loadResource(Rez.Fonts.led_small_lines);
            fontSmallData = loadFontVariant(Rez.Fonts.led, Rez.Fonts.led_inbetween, Rez.Fonts.led_lines, propSmallFontVariant);
            fontBottomData = loadFontVariant(Rez.Fonts.led_big, Rez.Fonts.led_big_readable, Rez.Fonts.led_big_lines, propBottomFontVariant);
            fontLabel = Application.loadResource(Rez.Fonts.storre);
            fontAODData = Application.loadResource(Rez.Fonts.led);
            fontBattery = fontTinyData;

            textSideAdj = 4;
            marginY = 13;
            labelHeight = 10;
            smallDataHeight = 20;
            bottomFiveAdj = 4;
            baseY = centerY - smallDataHeight - 4;
            bottomFieldWidths = [4, 4, 4, 0];
        } else {
            fontTinyData = Application.loadResource(Rez.Fonts.led_small_lines);
            fontSmallData = loadFontVariant(Rez.Fonts.led_big, Rez.Fonts.led_big_readable, Rez.Fonts.led_big_lines, propSmallFontVariant);
            fontBottomData = loadFontVariant(Rez.Fonts.led_big, Rez.Fonts.led_big_readable, Rez.Fonts.led_big_lines, propBottomFontVariant);
            fontLabel = fontTinyData;
            fontAODData = Application.loadResource(Rez.Fonts.led_big);
            fontBattery = Application.loadResource(Rez.Fonts.led_lines);

            textSideAdj = 2;
            marginY = 12;
            labelHeight = 13;
            smallDataHeight = 27;
            bottomFiveAdj = 6;
            baseY = centerY - 5;
            bottomFieldWidths = [4, 3, 4, 0];
        }

        loadAODGraphics();

        clockHeight = 125;
        clockWidth = 360;
        labelMargin = 8;
        tinyDataHeight = 13;
        largeDataHeight = 27;
        largeDataWidth = 24;
        bottomDataWidth = 24;
        baseX = centerX;
        barBottomAdj = 2;
        bottomFiveAdj = 8;
        histogramHeight = (propHistogramSize == 1) ? 35 : 25;
    }

    (:Round454)
    hidden function loadResources() as Void {
        switch(propClockFont) {
            case 1:  fontClock = Application.loadResource(Rez.Fonts.segments145_2); fontClockOutline = Application.loadResource(Rez.Fonts.segments145outline_2); break;
            case 2:  fontClock = Application.loadResource(Rez.Fonts.segments145_3); fontClockOutline = Application.loadResource(Rez.Fonts.segments145outline_3); break;
            case 3:  fontClock = Application.loadResource(Rez.Fonts.segments145_4); fontClockOutline = Application.loadResource(Rez.Fonts.segments145outline_4); break;
            case 4:  fontClock = Application.loadResource(Rez.Fonts.segments145_2r); fontClockOutline = Application.loadResource(Rez.Fonts.segments145outline_2r); break;
            default: fontClock = Application.loadResource(Rez.Fonts.segments145);   fontClockOutline = Application.loadResource(Rez.Fonts.segments145outline); break;
        }

        if(propFontSize == 0) {
            fontTinyData = Application.loadResource(Rez.Fonts.led_small_lines);
            fontSmallData = loadFontVariant(Rez.Fonts.led, Rez.Fonts.led_inbetween, Rez.Fonts.led_lines, propSmallFontVariant);
            fontBottomData = loadFontVariant(Rez.Fonts.led_big, Rez.Fonts.led_big_readable, Rez.Fonts.led_big_lines, propBottomFontVariant);
            fontLabel = Application.loadResource(Rez.Fonts.storre);
            fontAODData = Application.loadResource(Rez.Fonts.led);
            fontBattery = fontTinyData;

            textSideAdj = 1;
            marginY = 15;
            labelHeight = 10;
            smallDataHeight = 20;
            bottomFiveAdj = 8;
            baseY = centerY - smallDataHeight;
            bottomFieldWidths = [4, 4, 4, 0];

        } else {
            fontTinyData = Application.loadResource(Rez.Fonts.led_small_lines);
            fontSmallData = loadFontVariant(Rez.Fonts.led_big, Rez.Fonts.led_big_readable, Rez.Fonts.led_big_lines, propSmallFontVariant);
            fontBottomData = loadFontVariant(Rez.Fonts.led_big, Rez.Fonts.led_big_readable, Rez.Fonts.led_big_lines, propBottomFontVariant);
            fontLabel = fontTinyData;
            fontAODData = Application.loadResource(Rez.Fonts.led_big);
            fontBattery = Application.loadResource(Rez.Fonts.led_lines);

            textSideAdj = 6;
            marginY = 14;
            labelHeight = 13;
            smallDataHeight = 27;
            bottomFiveAdj = 6;
            baseY = centerY - 5;
            bottomFieldWidths = [3, 3, 3, 0];
        }

        clockHeight = 145;
        clockWidth = 415;
        loadAODGraphics();

        labelMargin = 8;
        tinyDataHeight = 13;
        largeDataHeight = 27;
        baseX = centerX + 3;
        fieldSpaceingAdj = 20;
        largeDataWidth = 24;
        bottomDataWidth = 24;
        barBottomAdj = 2;
        histogramHeight = (propHistogramSize == 1) ? 40 : 30;
        histogramTargetWidth = 45;
    }

    (:Square)
    hidden function loadResources() as Void {
        switch(propClockFont) {
            case 1:  fontClock = Application.loadResource(Rez.Fonts.segments145_2); fontClockOutline = Application.loadResource(Rez.Fonts.segments145outline_2); break;
            case 2:  fontClock = Application.loadResource(Rez.Fonts.segments145_3); fontClockOutline = Application.loadResource(Rez.Fonts.segments145outline_3); break;
            case 3:  fontClock = Application.loadResource(Rez.Fonts.segments145_4); fontClockOutline = Application.loadResource(Rez.Fonts.segments145outline_4); break;
            case 4:  fontClock = Application.loadResource(Rez.Fonts.segments145_2r); fontClockOutline = Application.loadResource(Rez.Fonts.segments145outline_2r); break;
            default: fontClock = Application.loadResource(Rez.Fonts.segments145);   fontClockOutline = Application.loadResource(Rez.Fonts.segments145outline); break;
        }

        if(propFontSize == 0) {
            fontTinyData = Application.loadResource(Rez.Fonts.led_small_lines);
            fontSmallData = loadFontVariant(Rez.Fonts.led, Rez.Fonts.led_inbetween, Rez.Fonts.led_lines, propSmallFontVariant);
            fontBottomData = loadFontVariant(Rez.Fonts.led_big, Rez.Fonts.led_big_readable, Rez.Fonts.led_big_lines, propBottomFontVariant);
            fontLabel = Application.loadResource(Rez.Fonts.storre);
            fontAODData = Application.loadResource(Rez.Fonts.led);
            fontBattery = fontTinyData;

            textSideAdj = 1;
            marginY = 18;
            labelHeight = 10;
            smallDataHeight = 20;
            bottomFiveAdj = 8;
            baseY = centerY - smallDataHeight - 10;
            bottomFieldWidths = [4, 4, 4, 0];

        } else {
            fontTinyData = Application.loadResource(Rez.Fonts.led_small_lines);
            fontSmallData = loadFontVariant(Rez.Fonts.led_big, Rez.Fonts.led_big_readable, Rez.Fonts.led_big_lines, propSmallFontVariant);
            fontBottomData = loadFontVariant(Rez.Fonts.led_big, Rez.Fonts.led_big_readable, Rez.Fonts.led_big_lines, propBottomFontVariant);
            fontLabel = fontTinyData;
            fontAODData = Application.loadResource(Rez.Fonts.led_big);
            fontBattery = Application.loadResource(Rez.Fonts.led_lines);

            textSideAdj = 6;
            marginY = 14;
            labelHeight = 13;
            smallDataHeight = 27;
            bottomFiveAdj = 6;
            baseY = centerY - 35;
            bottomFieldWidths = [4, 4, 4, 0];
        }
        
        clockHeight = 145;
        clockWidth = 415;
        loadAODGraphics();

        labelMargin = 8;
        tinyDataHeight = 13;
        largeDataHeight = 27;
        baseX = centerX + 3;
        fieldSpaceingAdj = 20;
        largeDataWidth = 24;
        bottomDataWidth = 24;
        barBottomAdj = 2;
        histogramHeight = (propHistogramSize == 1) ? 40 : 30;
        histogramTargetWidth = 45;
    }

    hidden function computeDisplayValues(now as Gregorian.Info) as Dictionary {
        var values = {};
        
        // From updateSlowData logic
        values[:dataClock] = getClockData(now);
        values[:dataMoon] = (propTopPartShows == 0) ? moonPhase(now) : "";
        if(propTopPartShows == 2) {
            values[:dataGraph1] = getDataArrayByType(propHistogramData);
        } else {
            values[:dataGraph1] = null;
        }

        values[:dataLabelTopLeft] = strLabelTopLeft;
        values[:dataLabelTopRight] = strLabelTopRight;
        values[:dataLabelBottomLeft] = strLabelBottomLeft;
        values[:dataLabelBottomMiddle] = strLabelBottomMiddle;
        values[:dataLabelBottomRight] = strLabelBottomRight;
        values[:dataLabelBottomFourth] = strLabelBottomFourth;

        // From updateData logic
        var fieldWidths = getFieldWidths();
        values[:dataTopLeft] = getValueByType(propSunriseFieldShows, 5);
        values[:dataTopRight] = getValueByType(propSunsetFieldShows, 5);
        values[:dataAboveLine1] = getValueByTypeWithUnit(propWeatherLine1Shows, 10);
        values[:dataAboveLine2] = getValueByTypeWithUnit(propWeatherLine2Shows, 10);
        values[:dataBelow] = getValueByTypeWithUnit(propDateFieldShows, 10);
        values[:dataNotifications] = getValueByType(propNotificationCountShows, 2);
        values[:dataBottomLeft] = getValueByType(propLeftValueShows, fieldWidths[0]);
        values[:dataBottomMiddle] = getValueByType(propMiddleValueShows, fieldWidths[1]);
        values[:dataBottomRight] = getValueByType(propRightValueShows, fieldWidths[2]);
        values[:dataBottomFourth] = getValueByType(propFourthValueShows, fieldWidths[3]);
        values[:dataBottom] = getValueByType(propBottomFieldShows, 5);
        computeBottomField2Values(values);
        values[:dataIcon1] = getIconState(propIcon1);
        values[:dataIcon2] = getIconState(propIcon2);
        values[:dataBattery] = getBattData();
        values[:dataAODLeft] = getValueByType(propAodFieldShows, 10);
        values[:dataAODRight] = getValueByType(propAodRightFieldShows, 5);
        values[:dataLeftBar] = getBarData(propLeftBarShows);
        values[:dataRightBar] = getBarData(propRightBarShows);

        if(!infoMessage.length() == 0) {
            values[:dataBelow] = infoMessage;
            infoMessage = ""; 
        }
        
        values[:dataSeconds] = getValueForSeconds(now);
        
        return values;
    }

    // Load your resources here
    function onLayout(dc as Dc) as Void {
    }

    // Called when this View is brought to the foreground.
    // Restore the state of this View and prepare it to be shown.
    // This includes loading resources into memory.
    function onShow() as Void {
        visible = true;
        lastUpdate = null;
        lastSlowUpdate = null;
    }

    // Update the view
    function onUpdate(dc as Dc) as Void {
        if(!visible) { return; }

        var now = Time.Gregorian.info(Time.now(), Time.FORMAT_SHORT);
        var unix_timestamp = Time.now().value();

        if(doesPartialUpdate) {
            dc.clearClip();
            doesPartialUpdate = false;
        }

        if(now.sec % 60 == 0 or lastSlowUpdate == null or unix_timestamp - lastSlowUpdate >= 60) {
            lastSlowUpdate = unix_timestamp;
            updateColorTheme();
            updateWeather();
        }

        if(lastUpdate == null or unix_timestamp - lastUpdate >= propUpdateFreq) {
            lastUpdate = unix_timestamp;
            cachedValues = computeDisplayValues(now);
        } else {
            // Only update time-sensitive values
            cachedValues[:dataClock] = getClockData(now);
            cachedValues[:dataSeconds] = getValueForSeconds(now);
        }

        if(isSleeping and canBurnIn) {
            drawAOD(dc, now, cachedValues);
        } else {
            drawWatchface(dc, now, false, cachedValues);
        }
    }

    // Called when this View is removed from the screen.
    // Save the state of this View here.
    // This includes freeing resources from memory.
    function onHide() as Void {
        visible = false;
    }

    // The user has just looked at their watch. Timers and animations may be started here.
    function onExitSleep() as Void {
        lastUpdate = null;
        lastSlowUpdate = null;
        isSleeping = false;
        WatchUi.requestUpdate();
    }

    // Terminate any active timers and prepare for slow updates.
    function onEnterSleep() as Void {
        lastUpdate = null;
        lastSlowUpdate = null;
        isSleeping = true;
        WatchUi.requestUpdate();
    }

    function onSettingsChanged() as Void {
        reloadSettings();
        lastUpdate = null;
        lastSlowUpdate = null;
        WatchUi.requestUpdate();
    }

    function onPartialUpdate(dc) {
        if(canBurnIn) { return; }
        if(!propAlwaysShowSeconds) { return; }
        doesPartialUpdate = true;

        var now = Time.Gregorian.info(Time.now(), Time.FORMAT_SHORT);
        var y1 = baseY + halfClockHeight + marginY;

        var seconds = now.sec.format("%02d");

        dc.setClip(baseX + halfClockWidth - textSideAdj - secondsClipWidth, y1, secondsClipWidth, smallDataHeight+1);
        dc.setColor(themeColors[bg], themeColors[bg]);
        dc.clear();

        dc.setColor(themeColors[date], Graphics.COLOR_TRANSPARENT);
        dc.drawText(baseX + halfClockWidth - textSideAdj, y1, fontSmallData, seconds, Graphics.TEXT_JUSTIFY_RIGHT);
    }

    hidden function calculateLayout() as Void {
        var y1 = baseY + halfClockHeight + marginY;
        var y2 = y1 + smallDataHeight + marginY;
        var y3 = y2 + labelHeight + labelMargin + largeDataHeight;
        
        fieldY = y2;
        
        var data_width = Math.sqrt(centerY*centerY - (y3 - centerY)*(y3 - centerY)) * 2 + fieldSpaceingAdj;
        var left_edge = Math.round((screenWidth - data_width) / 2);
        
        calculateFieldXCoords(data_width, left_edge);

        bottomFiveY = y3 + halfMarginY + bottomFiveAdj;
        if((propLabelVisibility == 1 or propLabelVisibility == 3)) { bottomFiveY = bottomFiveY - labelHeight; }
        calculateSquareLayout();
    }

    hidden function calculateFieldXCoords(data_width as Float, left_edge as Number) as Void {
        var digits = getFieldWidths();
        var tot_digits = digits[0] + digits[1] + digits[2] + digits[3];
        if (tot_digits == 0) { return; }

        // Compute each field center in a single expression to avoid accumulated rounding errors.
        // This keeps symmetric layouts (e.g. 3-5-3) perfectly centered relative to data_width.
        var d0 = digits[0].toFloat();
        var d1 = digits[1].toFloat();
        var d2 = digits[2].toFloat();
        var d3 = digits[3].toFloat();
        var tot = tot_digits.toFloat();

        fieldXCoords[0] = left_edge + Math.round((d0 / 2.0) * data_width / tot);
        fieldXCoords[1] = left_edge + Math.round((d0 + d1 / 2.0) * data_width / tot);
        fieldXCoords[2] = left_edge + Math.round((d0 + d1 + d2 / 2.0) * data_width / tot);
        fieldXCoords[3] = left_edge + Math.round((d0 + d1 + d2 + d3 / 2.0) * data_width / tot);
    }

    hidden function drawWatchface(dc as Dc, now as Gregorian.Info, aod as Boolean, values as Dictionary) as Void {
        // Clear
        dc.setColor(themeColors[bg], themeColors[bg]);
        dc.clear();
        var yn1 = baseY - halfClockHeight - marginY - smallDataHeight;
        var yn2 = yn1 - marginY - smallDataHeight;
        var yn3 = yn2 - marginY - labelHeight - tinyDataHeight - halfMarginY - aboveLine2Adjustment;

        // Draw Top data fields or histogram
        if(propTopPartShows == 2) {
            yn3 = yn2 - marginY - histogramHeight;
            drawHistogram(dc, values[:dataGraph1], centerX, yn3, histogramHeight);
        } else {
            var top_data_height = marginY;
            var top_field_font = fontTinyData;
            var top_field_center_offset = 20;
            if(propTopPartShows == 1) { top_field_center_offset = labelHeight; }
            if(propLabelVisibility == 0 or propLabelVisibility == 3) {
                // Top 2 fields: Labels
                dc.setColor(themeColors[fieldLbl], Graphics.COLOR_TRANSPARENT);
                dc.drawText(centerX - top_field_center_offset, yn3, fontLabel, values[:dataLabelTopLeft], Graphics.TEXT_JUSTIFY_RIGHT);
                dc.drawText(centerX + top_field_center_offset, yn3, fontLabel, values[:dataLabelTopRight], Graphics.TEXT_JUSTIFY_LEFT);

                top_data_height = labelHeight + halfMarginY;
            }

            dc.setColor(themeColors[dataVal], Graphics.COLOR_TRANSPARENT);
            if(propTopPartShows == 0) {
                // Top 2 fields: Values
                dc.drawText(centerX - top_field_center_offset, yn3 + top_data_height, top_field_font, values[:dataTopLeft], Graphics.TEXT_JUSTIFY_RIGHT);
                dc.drawText(centerX + top_field_center_offset, yn3 + top_data_height, top_field_font, values[:dataTopRight], Graphics.TEXT_JUSTIFY_LEFT);

                // Draw Moon
                dc.setColor(themeColors[moon], Graphics.COLOR_TRANSPARENT);
                dc.drawText(centerX, yn3 + ((top_data_height + tinyDataHeight) / 2) + 2, fontMoon, values[:dataMoon], Graphics.TEXT_JUSTIFY_CENTER | Graphics.TEXT_JUSTIFY_VCENTER);
            } else {
                // Top 2 fields: Just values (larger)
                if(top_data_height == marginY) { top_field_font = fontSmallData; }
                dc.drawText(centerX - top_field_center_offset, yn3 + top_data_height, top_field_font, values[:dataTopLeft], Graphics.TEXT_JUSTIFY_RIGHT);
                dc.drawText(centerX + top_field_center_offset, yn3 + top_data_height, top_field_font, values[:dataTopRight], Graphics.TEXT_JUSTIFY_LEFT);
            }
        }

        // Draw Lines above clock
        dc.setColor(themeColors[dataVal], Graphics.COLOR_TRANSPARENT);
        dc.drawText(centerX, yn2, fontSmallData, values[:dataAboveLine1], Graphics.TEXT_JUSTIFY_CENTER);
        dc.drawText(centerX, yn1, fontSmallData, values[:dataAboveLine2], Graphics.TEXT_JUSTIFY_CENTER);        

        if(!aod) {
            // Draw Clock
            if(propClockOutlineStyle != 5) {
                dc.setColor(themeColors[clockBg], Graphics.COLOR_TRANSPARENT);
                if(propShowClockBg and !aod) {
                    dc.drawText(baseX, baseY, fontClock, clockBgText, Graphics.TEXT_JUSTIFY_CENTER | Graphics.TEXT_JUSTIFY_VCENTER);
                }
                dc.setColor(themeColors[clock], Graphics.COLOR_TRANSPARENT);
                dc.drawText(baseX, baseY, fontClock, values[:dataClock], Graphics.TEXT_JUSTIFY_CENTER | Graphics.TEXT_JUSTIFY_VCENTER);

                // Draw clock gradient
                if(drawGradient != null and themeColors[bg] == 0x000000 and !aod) {
                    dc.drawBitmap(centerX - halfClockWidth, baseY - halfClockHeight, drawGradient);
                }
            }

            if(propClockOutlineStyle == 2 or propClockOutlineStyle == 3 or propClockOutlineStyle == 5) {
                if(fontClockOutline != null) { // Someone has only bothered to draw this font for AMOLED sizes
                    // Draw outline
                    dc.setColor(themeColors[outline], Graphics.COLOR_TRANSPARENT);
                    dc.drawText(baseX, baseY, fontClockOutline, values[:dataClock], Graphics.TEXT_JUSTIFY_CENTER | Graphics.TEXT_JUSTIFY_VCENTER);
                }
            }
        } else { // AOD
            if(propClockOutlineStyle == 0 or propClockOutlineStyle == 2) {
                // Draw Clock
                dc.setColor(themeColors[clock], Graphics.COLOR_TRANSPARENT);
                dc.drawText(baseX, baseY, fontClock, values[:dataClock], Graphics.TEXT_JUSTIFY_CENTER | Graphics.TEXT_JUSTIFY_VCENTER);
            }

            if(propClockOutlineStyle == 1 or propClockOutlineStyle == 2 or propClockOutlineStyle == 3 or propClockOutlineStyle == 5) {
                // Draw Outline
                dc.setColor(themeColors[outline], Graphics.COLOR_TRANSPARENT);
                dc.drawText(baseX, baseY, fontClockOutline, values[:dataClock], Graphics.TEXT_JUSTIFY_CENTER | Graphics.TEXT_JUSTIFY_VCENTER);
            }

            if(propClockOutlineStyle == 4) {
                // Filled clock but outline color
                dc.setColor(themeColors[outline], Graphics.COLOR_TRANSPARENT);
                dc.drawText(baseX, baseY, fontClock, values[:dataClock], Graphics.TEXT_JUSTIFY_CENTER | Graphics.TEXT_JUSTIFY_VCENTER);
            }
        }

        // Draw stress and body battery bars
        drawSideBars(dc, values);

        // Draw Line below clock
        var y1 = baseY + halfClockHeight + marginY;
        dc.setColor(themeColors[date], Graphics.COLOR_TRANSPARENT);
        if(propDateAlignment == 0) {
            dc.drawText(baseX - halfClockWidth + textSideAdj, y1, fontSmallData, values[:dataBelow], Graphics.TEXT_JUSTIFY_LEFT);
        } else {
            dc.drawText(baseX, y1, fontSmallData, values[:dataBelow], Graphics.TEXT_JUSTIFY_CENTER);
        }
        
        // Draw seconds
        if(propShowSeconds) {
            dc.drawText(baseX + halfClockWidth - textSideAdj, y1, fontSmallData, values[:dataSeconds], Graphics.TEXT_JUSTIFY_RIGHT);
        }

        // Draw Notification count
        dc.setColor(themeColors[notif], Graphics.COLOR_TRANSPARENT);
        if(propDateAlignment == 0) {
            if(!propShowSeconds) { // No seconds, notification on right side
                dc.drawText(baseX + halfClockWidth - textSideAdj, y1, fontSmallData, values[:dataNotifications], Graphics.TEXT_JUSTIFY_RIGHT);
            } else {
                var date_width = dc.getTextWidthInPixels(values[:dataBelow], fontSmallData);
                var sec_width = dc.getTextWidthInPixels(values[:dataSeconds], fontSmallData); 
                var date_right_edge = baseX - halfClockWidth + textSideAdj + date_width;
                var sec_left = baseX + halfClockWidth - textSideAdj - sec_width;
                var pos = sec_left - marginX;
                if((sec_left - date_right_edge) < 3 * marginX) {
                    pos = (date_right_edge + sec_left) / 2;
                }
                dc.drawText(pos, y1, fontSmallData, values[:dataNotifications], Graphics.TEXT_JUSTIFY_CENTER);
            }
        } else { // Date is centered, notification on left side
            dc.drawText(baseX - halfClockWidth, y1, fontSmallData, values[:dataNotifications], Graphics.TEXT_JUSTIFY_LEFT);
        }

        // Draw the three bottom data fields
        var digits = getFieldWidths();

        drawDataField(dc, fieldXCoords[0], fieldY, 3, values[:dataLabelBottomLeft], values[:dataBottomLeft], digits[0], fontBottomData, largeDataWidth * digits[0]);
        drawDataField(dc, fieldXCoords[1], fieldY, 3, values[:dataLabelBottomMiddle], values[:dataBottomMiddle], digits[1], fontBottomData, largeDataWidth * digits[1]);
        drawDataField(dc, fieldXCoords[2], fieldY, 3, values[:dataLabelBottomRight], values[:dataBottomRight], digits[2], fontBottomData, largeDataWidth * digits[2]);
        drawDataField(dc, fieldXCoords[3], fieldY, 3, values[:dataLabelBottomFourth], values[:dataBottomFourth], digits[3], fontBottomData, largeDataWidth * digits[3]);

        // Draw the 5 digit bottom field(s) and icons
        drawBottomFieldsWithIcons(dc, values);

        // Draw battery icon
        drawBatteryIcon(dc, values);
    }

    (:MIP)
    hidden function drawAOD(dc as Dc, now as Gregorian.Info, values as Dictionary) as Void { }

    (:AMOLED)
    hidden function drawAOD(dc as Dc, now as Gregorian.Info, values as Dictionary) as Void {
        dc.setColor(0x000000, 0x000000);
        dc.clear();

        if(propAodStyle == 2) {
            drawWatchface(dc, now, true, values);
            drawPattern(dc, 0x000000, (now.min % 3));
        } else if (propAodStyle == 1) {
            if(propClockOutlineStyle == 0 or propClockOutlineStyle == 2) {
                // Draw Clock
                dc.setColor(themeColors[clock], Graphics.COLOR_TRANSPARENT);
                dc.drawText(baseX, baseY, fontClock, values[:dataClock], Graphics.TEXT_JUSTIFY_CENTER | Graphics.TEXT_JUSTIFY_VCENTER);
            }

            if(propClockOutlineStyle == 1 or propClockOutlineStyle == 2 or propClockOutlineStyle == 3 or propClockOutlineStyle == 5) {
                // Draw Outline
                dc.setColor(themeColors[outline], Graphics.COLOR_TRANSPARENT);
                dc.drawText(baseX, baseY, fontClockOutline, values[:dataClock], Graphics.TEXT_JUSTIFY_CENTER | Graphics.TEXT_JUSTIFY_VCENTER);
            }

            if(propClockOutlineStyle == 4) {
                // Filled clock but outline color
                dc.setColor(themeColors[outline], Graphics.COLOR_TRANSPARENT);
                dc.drawText(baseX, baseY, fontClock, values[:dataClock], Graphics.TEXT_JUSTIFY_CENTER | Graphics.TEXT_JUSTIFY_VCENTER);
            }

            // Draw clock gradient
            if(propClockGradientOverlay == 4 or propClockGradientOverlay == 5) {
                dc.drawBitmap(centerX - halfClockWidth, baseY - halfClockHeight - (now.min % 2), drawAODPattern);
            } else {
                dc.drawBitmap(centerX - halfClockWidth - (now.min % 2), baseY - halfClockHeight, drawAODPattern);
            }
            

            // Draw Line below clock
            var y1 = baseY + halfClockHeight + marginY;
            dc.setColor(themeColors[dateDim], Graphics.COLOR_TRANSPARENT);
            if(propAodAlignment == 0) {
                dc.drawText(baseX - halfClockWidth + textSideAdj - (now.min % 3), y1, fontAODData, values[:dataAODLeft], Graphics.TEXT_JUSTIFY_LEFT);
            } else {
                dc.drawText(baseX - (now.min % 3), y1, fontAODData, values[:dataAODLeft], Graphics.TEXT_JUSTIFY_CENTER);
            }
            dc.drawText(baseX + halfClockWidth - textSideAdj - 2 - (now.min % 3), y1, fontAODData, values[:dataAODRight], Graphics.TEXT_JUSTIFY_RIGHT);
        }
    }

    (:AMOLED)
    hidden function drawPattern(dc as Dc, color as ColorType, offset as Number) as Void {
        var text = "";
        var pattern = "S"; // Checkerboard
        if(propClockGradientOverlay == 4 or propClockGradientOverlay == 5) { pattern = "U"; } // Scanlines

        for(var i = 0; i < Math.ceil(screenWidth / 20) + 1; i++) {
                text += pattern;
        }

        dc.setColor(color, Graphics.COLOR_TRANSPARENT);
        var i = 0;
        while(i < Math.ceil(screenHeight / 20) + 1) {
            dc.drawText(0, i*20 + offset, fontIcons, text, Graphics.TEXT_JUSTIFY_LEFT);
            i++;
        }
    }

    hidden function getFieldWidths() as Array<Number> {
        if(propFieldLayout == 0) { // Auto
            return bottomFieldWidths;
        } else if(propFieldLayout == 1) {
            return [3, 3, 3, 0];
        } else if(propFieldLayout == 2) {
            return [3, 4, 3, 0];
        } else if(propFieldLayout == 3) {
            return [3, 3, 4, 0];
        } else if(propFieldLayout == 4) {
            return [4, 3, 3, 0];
        } else if(propFieldLayout == 5) {
            return [4, 3, 4, 0];
        } else if(propFieldLayout == 6) {
            return [3, 4, 4, 0];
        } else if(propFieldLayout == 7) {
            return [4, 4, 3, 0];
        } else if(propFieldLayout == 8) {
            return [4, 4, 4, 0];
        } else if(propFieldLayout == 9) {
            return [3, 3, 3, 3];
        } else if(propFieldLayout == 10) {
            return [3, 3, 3, 4];
        } else if(propFieldLayout == 11) {
            return [4, 3, 3, 3];
        } else if(propFieldLayout == 12) {
            return [4, 4, 0, 0];
        } else if(propFieldLayout == 13) {
            return [5, 3, 3, 0];
        } else if(propFieldLayout == 14) {
            return [5, 0, 0, 0];
        } else if(propFieldLayout == 15) {
            return [5, 5, 0, 0];
        } else {
            return [3, 5, 3, 0];
        }
    }

    hidden function drawDataField(dc as Dc, x as Number, y as Number, adjX as Number, label as String?, value as String, width as Number, font as FontResource, bgwidth as Number) as Number {
        if(value.length() == 0 and (label == null or label.length() == 0)) { return 0; }
        if(width == 0) { return 0; }
        var valueBg = "";
        var bgChar = "#";
        if(screenHeight == 360 and width == 5 and label == null) { bgChar = "$"; }
        for(var i=0; i<width; i++) { valueBg += bgChar; }

        var value_bg_width = bgwidth;//dc.getTextWidthInPixels(valueBg, font);
        var half_bg_width = Math.round(value_bg_width / 2);
        var data_y = y;

        if((propLabelVisibility == 0 or propLabelVisibility == 2) and !(label == null)) {
            dc.setColor(themeColors[fieldLbl], Graphics.COLOR_TRANSPARENT);
            if(propBottomFieldLabelAlignment == 0) {
                dc.drawText(x - half_bg_width + adjX, y, fontLabel, label, Graphics.TEXT_JUSTIFY_LEFT);
            } else {
                dc.drawText(x, y, fontLabel, label, Graphics.TEXT_JUSTIFY_CENTER);
            }
            data_y += labelHeight + labelMargin;
        }

        if(propShowDataBg) {
            dc.setColor(themeColors[fieldBg], Graphics.COLOR_TRANSPARENT);
            dc.drawText(x - half_bg_width + adjX, data_y, font, valueBg, Graphics.TEXT_JUSTIFY_LEFT);
        }

        dc.setColor(themeColors[dataVal], Graphics.COLOR_TRANSPARENT);
        if(propBottomFieldAlignment == 0) {
            dc.drawText(x - half_bg_width + adjX, data_y, font, value, Graphics.TEXT_JUSTIFY_LEFT);
        } else if (propBottomFieldAlignment == 1) {
            dc.drawText(x + adjX, data_y, font, value, Graphics.TEXT_JUSTIFY_CENTER);
        } else if (propBottomFieldAlignment == 2) {
            dc.drawText(x + half_bg_width - 1 + adjX, data_y, font, value, Graphics.TEXT_JUSTIFY_RIGHT);
        } else if (propBottomFieldAlignment == 3 and width != 5) {
            dc.drawText(x - half_bg_width + adjX, data_y, font, value, Graphics.TEXT_JUSTIFY_LEFT);
        } else if (propBottomFieldAlignment == 3 and width == 5) {
            dc.drawText(x + adjX, data_y, font, value, Graphics.TEXT_JUSTIFY_CENTER);
        }

        return value_bg_width;
    }

    hidden function drawSideBars(dc as Dc, values as Dictionary) as Void {
        var barVal;
        var barHeight;
        var barColor;

        if (values[:dataLeftBar] != null) {
            barVal = values[:dataLeftBar];
            barHeight = Math.round(barVal * (clockHeight / 100.0));
            if (propLeftBarShows == 1 && propStressDynamicColor) {
                barColor = getStressColor(barVal);
            } else {
                barColor = themeColors[stress]; 
            }
            dc.setColor(barColor, Graphics.COLOR_TRANSPARENT);
            dc.fillRectangle(
                centerX - halfClockWidth - barWidth - barWidth, baseY + halfClockHeight - barHeight + barBottomAdj, barWidth, barHeight
            );

            if(propLeftBarShows == 6) {
                drawMoveBarTicks(dc, centerX - halfClockWidth - barWidth - barWidth, centerX - halfClockWidth);
            }
        }

        if (values[:dataRightBar] != null) {
            barVal = values[:dataRightBar];
            barHeight = Math.round(barVal * (clockHeight / 100.0));
            if (propRightBarShows == 1 && propStressDynamicColor) {
                barColor = getStressColor(barVal);
            } else {
                barColor = themeColors[bodybatt]; 
            }
            dc.setColor(barColor, Graphics.COLOR_TRANSPARENT);
            dc.fillRectangle(
                centerX + halfClockWidth + barWidth, baseY + halfClockHeight - barHeight + barBottomAdj, barWidth, barHeight
            );
            
            if(propRightBarShows == 6) {
                drawMoveBarTicks(dc, centerX + halfClockWidth + barWidth + barWidth, centerX + halfClockWidth);
            }
        }
    }

    hidden function drawMoveBarTicks(dc as Dc, x1, x2) as Void {
        dc.setColor(themeColors[bg], Graphics.COLOR_TRANSPARENT);
        dc.setPenWidth(2);
        dc.drawLine(x1, baseY + halfClockHeight - (40 * (clockHeight / 100.0)), x2, baseY + halfClockHeight - (40 * (clockHeight / 100.0)));
        dc.drawLine(x1, baseY + halfClockHeight - (55 * (clockHeight / 100.0)), x2, baseY + halfClockHeight - (55 * (clockHeight / 100.0)));
        dc.drawLine(x1, baseY + halfClockHeight - (70 * (clockHeight / 100.0)), x2, baseY + halfClockHeight - (70 * (clockHeight / 100.0)));
        dc.drawLine(x1, baseY + halfClockHeight - (85 * (clockHeight / 100.0)), x2, baseY + halfClockHeight - (85 * (clockHeight / 100.0)));
        dc.setPenWidth(1);
    }

    hidden function drawHistogram(dc as Dc, data as Array<Number>?, x as Number, y as Number, h as Number) as Void {
        if(data == null) { return; }
        var scale = 100.0 / h;
        var half_width = Math.round((data.size() * (histogramBarWidth + histogramBarSpacing)) / 2);
        var bar_height = 0;

        dc.setColor(themeColors[clock], Graphics.COLOR_TRANSPARENT);
        for(var i=0; i<data.size(); i++) {
            if(data[i] == null) { break; }
            if(propHistogramData == 7) {
                dc.setColor(getStressColor(data[i]), Graphics.COLOR_TRANSPARENT);
            }
            bar_height = Math.round(data[i] / scale);
            dc.fillRectangle(x - half_width + i * (histogramBarWidth + histogramBarSpacing), y + (h - bar_height), histogramBarWidth, bar_height);
        }
    }

    (:AMOLED)
    hidden function drawBatteryIcon(dc as Dc, values as Dictionary) {
        if(propBatteryVariant == 2) { return; }
        if(propBatteryVariant == -1 and propFontSize == 1 and (propBottomFieldShows != -2 or propBottomField2Shows != -2)) { return; } // Auto - hide if large font and bottom field is shown
        var x = centerX;
        var y =  screenHeight - 25;
        dc.setColor(0x555555, Graphics.COLOR_TRANSPARENT);

        if(propFontSize == 0) {
            dc.drawText(x, y, fontIcons, "C", Graphics.TEXT_JUSTIFY_CENTER);
            if(System.getSystemStats().battery <= 15) {
                dc.setColor(0xFF0000, Graphics.COLOR_TRANSPARENT);
            } else {
                dc.setColor(themeColors[dataVal], Graphics.COLOR_TRANSPARENT);
            }
            if(propBatteryVariant == 3 or propBatteryVariant == -1) {
                dc.drawText(x - 19, y + 4, fontBattery, values[:dataBattery], Graphics.TEXT_JUSTIFY_LEFT);
            } else { // centered when not a bar
                dc.drawText(x - 1, y + 4, fontBattery, values[:dataBattery], Graphics.TEXT_JUSTIFY_CENTER);
            }
        } else {
            y = screenHeight - 33;
            dc.drawText(x, y, fontIcons, "T", Graphics.TEXT_JUSTIFY_CENTER);
            if(System.getSystemStats().battery <= 15) {
                dc.setColor(0xFF0000, Graphics.COLOR_TRANSPARENT);
            } else {
                dc.setColor(themeColors[dataVal], Graphics.COLOR_TRANSPARENT);
            }
            if(propBatteryVariant == 3 or propBatteryVariant == -1) {
                dc.drawText(x - 26, y + 4, fontBattery, values[:dataBattery], Graphics.TEXT_JUSTIFY_LEFT);
            } else { // centered when not a bar
                dc.drawText(x - 1, y + 4, fontBattery, values[:dataBattery], Graphics.TEXT_JUSTIFY_CENTER);
            }
        }
        
        
    }

    (:MIP)
    hidden function drawBatteryIcon(dc as Dc, values as Dictionary) {
        if(propBatteryVariant == 2) { return; }
        if(propBatteryVariant == -1 and propFontSize == 1 and (propBottomFieldShows != -2 or propBottomField2Shows != -2)) { return; } // Auto - hide if large font and bottom field is shown
        var x = centerX;
        var y =  screenHeight - 20;
        dc.setColor(0x555555, Graphics.COLOR_TRANSPARENT);

        if(propFontSize == 0) {
            dc.drawText(x, y, fontIcons, "B", Graphics.TEXT_JUSTIFY_CENTER);
            if(System.getSystemStats().battery <= 15) {
                dc.setColor(0xFF0000, Graphics.COLOR_TRANSPARENT);
            } else {
                dc.setColor(themeColors[dataVal], Graphics.COLOR_TRANSPARENT);
            }
            if(propBatteryVariant == 3 or propBatteryVariant == -1) {
                dc.drawText(x - 11, y + 3, fontBattery, values[:dataBattery], Graphics.TEXT_JUSTIFY_LEFT);
            } else {
                dc.drawText(x - 1, y + 3, fontBattery, values[:dataBattery], Graphics.TEXT_JUSTIFY_CENTER);
            }
        } else {
            y = screenHeight - 28;
            dc.drawText(x, y, fontIcons, "C", Graphics.TEXT_JUSTIFY_CENTER);
            if(System.getSystemStats().battery <= 15) {
                dc.setColor(0xFF0000, Graphics.COLOR_TRANSPARENT);
            } else {
                dc.setColor(themeColors[dataVal], Graphics.COLOR_TRANSPARENT);
            }
            if(propBatteryVariant == 3 or propBatteryVariant == -1) {
                dc.drawText(x - 19, y + 4, fontBattery, values[:dataBattery], Graphics.TEXT_JUSTIFY_LEFT);
            } else {
                dc.drawText(x - 1, y + 4, fontBattery, values[:dataBattery], Graphics.TEXT_JUSTIFY_CENTER);
            }
        }
    }

    (:MIP)
    hidden function setColorTheme(theme as Number) as Array<Graphics.ColorType> {
        //                        bg,       clock,    clockBg,  outline,  dataVal,  fieldBg,  fieldLbl, date,     dateDim,  notif,    stress,   bodybatt, moon
        if(theme == 0 ) { return [0x000000, 0xFFFF00, 0x005555, 0xFFFF00, 0xFFFFFF, 0x005555, 0x55AAAA, 0xFFFF00, 0xa98753, 0x00AAFF, 0xFFAA00, 0x00AAFF, 0xFFFFFF]; } // Yellow on turquoise MIP
        if(theme == 1 ) { return [0x000000, 0xFF55AA, 0x005555, 0xFF55AA, 0xFFFFFF, 0x005555, 0xAA55AA, 0xFFFFFF, 0xa95399, 0xFF55AA, 0xFF55AA, 0x00FFAA, 0xFFFFFF]; } // Hot pink MIP
        if(theme == 2 ) { return [0x000000, 0x00FFFF, 0x0055AA, 0x00FFFF, 0xFFFFFF, 0x0055AA, 0x55AAAA, 0x00FFFF, 0x5ca28f, 0x00AAFF, 0xFFAA00, 0x00AAFF, 0xFFFFFF]; } // Blueish green MIP
        if(theme == 3 ) { return [0x000000, 0x00FF00, 0x005500, 0x00FF00, 0xFFFFFF, 0x005500, 0x00AA55, 0x00FF00, 0x5ca28f, 0x00AAFF, 0xFFAA00, 0x00AAFF, 0xFFFFFF]; } // Very green MIP
        if(theme == 4 ) { return [0x000000, 0xFFFFFF, 0x005555, 0xFFFFFF, 0xFFFFFF, 0x005555, 0x55AAAA, 0xFFFFFF, 0x114a5a, 0xAAAAAA, 0xFFAA55, 0x55AAFF, 0xFFFFFF]; } // White on turquoise MIP
        if(theme == 5 ) { return [0x000000, 0xFF5500, 0x5500AA, 0xFF5500, 0xFFFFFF, 0x5500AA, 0xFFAAAA, 0xFFAAAA, 0xaa6e56, 0xFFFFFF, 0xFF5555, 0x00AAFF, 0xFFFFFF]; } // Peachy Orange MIP
        if(theme == 6 ) { return [0x000000, 0xFFFFFF, 0xAA0000, 0xFFFFFF, 0xFFFFFF, 0xAA0000, 0xFF0000, 0xFFFFFF, 0xAA0000, 0xFF0000, 0xAA0000, 0x00AAFF, 0xFFFFFF]; } // Red and White MIP
        if(theme == 7 ) { return [0x000000, 0xFFFFFF, 0x0055AA, 0xFFFFFF, 0xFFFFFF, 0x0055AA, 0x0055AA, 0xFFFFFF, 0x0055AA, 0x55AAFF, 0xFFAA00, 0x55AAFF, 0xFFFFFF]; } // White on Blue MIP
        if(theme == 8 ) { return [0x000000, 0xFFAA00, 0x005555, 0xFFAA00, 0xFFFFFF, 0x005555, 0x55AAAA, 0xFFAA55, 0x555555, 0x55AAAA, 0xFFAA00, 0x55AAAA, 0xFFFFFF]; } // Orange on Teal MIP
        if(theme == 9 ) { return [0x000000, 0xFFFFFF, 0xaa5500, 0xFFFFFF, 0xFFFFFF, 0xaa5500, 0xFF5500, 0xFFFFFF, 0xAA5500, 0x00AAFF, 0xFFAA00, 0x00AAFF, 0xFFFFFF]; } // White and Orange MIP
        if(theme == 10) { return [0x000000, 0x0055AA, 0x000055, 0x0055AA, 0xFFFFFF, 0x555555, 0x0055AA, 0xFFFFFF, 0x0055AA, 0x55AAFF, 0xFFAA00, 0x55AAFF, 0xFFFFFF]; } // Blue MIP
        if(theme == 11) { return [0x000000, 0xFFAA00, 0x555555, 0xFFAA00, 0xFFFFFF, 0x555555, 0xFFAA00, 0xFFFFFF, 0x555555, 0x55AAFF, 0xFFAA00, 0x55AAFF, 0xFFFFFF]; } // Orange MIP
        if(theme == 12) { return [0x000000, 0xFFFFFF, 0x555555, 0xFFFFFF, 0xFFFFFF, 0x555555, 0xFFFFFF, 0xFFFFFF, 0x555555, 0x55AAFF, 0xFFAA00, 0x55AAFF, 0xFFFFFF]; } // White on black MIP
        if(theme == 13) { return [0xFFFFFF, 0x000000, 0xAAAAAA, 0x000000, 0x000000, 0xAAAAAA, 0x000000, 0x000000, 0x555555, 0x000000, 0xFFAA00, 0x55AAFF, 0x555555]; } // Black on White MIP
        if(theme == 14) { return [0xFFFFFF, 0xAA0000, 0xAAAAAA, 0xAA0000, 0x000000, 0xAAAAAA, 0xAA0000, 0x000000, 0x555555, 0x000000, 0xFFAA00, 0x55AAFF, 0x555555]; } // Red on White MIP
        if(theme == 15) { return [0xFFFFFF, 0x0000AA, 0xAAAAAA, 0x0000AA, 0x000000, 0xAAAAAA, 0x0000AA, 0x000000, 0x555555, 0x000000, 0xFFAA00, 0x55AAFF, 0x555555]; } // Blue on White MIP
        if(theme == 16) { return [0xFFFFFF, 0x00AA00, 0xAAAAAA, 0x00AA00, 0x000000, 0xAAAAAA, 0x00AA00, 0x000000, 0x555555, 0x000000, 0xFFAA00, 0x55AAFF, 0x555555]; } // Green on White MIP
        if(theme == 17) { return [0xFFFFFF, 0xFF5500, 0xAAAAAA, 0xFF5500, 0x000000, 0xAAAAAA, 0x555555, 0x000000, 0x555555, 0x000000, 0xFF5500, 0x55AAFF, 0x555555]; } // Orange on White MIP
        if(theme == 18) { return [0x000000, 0xFF5500, 0x005500, 0xFF5500, 0x00FF00, 0x005500, 0xFF5500, 0x00FF00, 0x5ca28f, 0x55FF55, 0xFF5500, 0x00AAFF, 0xFFFFFF]; } // Green and Orange MIP
        if(theme == 19) { return [0x000000, 0xAAAA55, 0x005500, 0xAAAA55, 0x00FF00, 0x005500, 0xAAAA00, 0xAAAA55, 0x546a36, 0x00FF55, 0xAAAA55, 0x00FF00, 0xFFFFFF]; } // Green Camo MIP
        if(theme == 20) { return [0x000000, 0xFF0000, 0x555555, 0xFF0000, 0xFFFFFF, 0x555555, 0xFF0000, 0xFFFFFF, 0x555555, 0x55AAFF, 0xFF5555, 0x55AAFF, 0xFFFFFF]; } // Red on Black MIP
        if(theme == 21) { return [0xFFFFFF, 0xAA00FF, 0xAAAAAA, 0xAA00FF, 0x000000, 0xAAAAAA, 0xAA00FF, 0x000000, 0x555555, 0x000000, 0xFF5500, 0x55AAFF, 0x555555]; } // Purple on White MIP
        if(theme == 22) { return [0x000000, 0xAA00FF, 0x555555, 0xAA00FF, 0xFFFFFF, 0x555555, 0xAA00FF, 0xFFFFFF, 0x555555, 0x55AAFF, 0xFFAA00, 0x55AAFF, 0xFFFFFF]; } // Purple on black MIP
        if(theme == 23) { return [0x000000, 0xFFAA00, 0x555555, 0xFFAA00, 0xFFAA55, 0x555555, 0xFFAA00, 0xFFAA55, 0x555555, 0x55AAAA, 0xFFAA00, 0x55AAAA, 0xFFFFFF]; } // Amber MIP
        if(theme == 30) { return parseThemeString(propColorOverride); }
        if(theme == 31) { return parseThemeString(propColorOverride2); }
        infoMessage = "THEME ERROR";
        return [0xff0000, 0x00ff00, 0x0000ff, 0x550000, 0x005500, 0x000055, 0xff00ff, 0x00ffff, 0xffff00, 0x005555, 0x550055, 0x555500, 0xffffff]; // error case
    }

    (:AMOLED)
    hidden function setColorTheme(theme as Number) as Array<Graphics.ColorType> {
        //                        bg,       clock,    clockBg,  outline,  dataVal,  fieldBg,  fieldLbl,   date,   dateDim,  notif,   stress,    bodybatt, moon
        if(theme == 0 ) { return [0x000000, 0xFBCB77, 0x0f2d34, 0xFFEAC4, 0xd5ffff, 0x0D333C, 0x61c6c6, 0xfacf83, 0xa89252, 0x00AAFF, 0xFFAA00, 0x00AAFF, 0xFFFFFF]; } // Yellow on turquoise AMOLED
        if(theme == 1 ) { return [0x000000, 0xff85c2, 0x0F3B46, 0xFFD9FC, 0xffe6f2, 0x0E333C, 0xff85c2, 0xffe6f2, 0xbf7498, 0xFF55AA, 0xFF55AA, 0x4cb2db, 0xFFFFFF]; } // Hot pink AMOLED
        if(theme == 2 ) { return [0x000000, 0x89EFD2, 0x0F2246, 0xB8EFDF, 0xdffff6, 0x0F2246, 0x69cece, 0x98efd6, 0x5CA28F, 0x00AAFF, 0xffcf98, 0x74d0fd, 0xFFFFFF]; } // Blueish green AMOLED
        if(theme == 3 ) { return [0x000000, 0x96E0AC, 0x292929, 0xC3E0CC, 0xe7ffee, 0x292929, 0x7bffbd, 0x96E0AC, 0x5CA28F, 0x00AAFF, 0xFFC884, 0x59B9FE, 0xFFFFFF]; } // Very green AMOLED
        if(theme == 4 ) { return [0x000000, 0xFFFFFF, 0x0d333c, 0xadeffe, 0xFFFFFF, 0x0e333c, 0x55AAAA, 0xFFFFFF, 0x1d7e99, 0xAAAAAA, 0xFFAA55, 0x55AAFF, 0xFFFFFF]; } // White on turquoise AMOLED
        if(theme == 5 ) { return [0x000000, 0xFF9161, 0x172135, 0xFFB494, 0xffeadd, 0x1B263D, 0xffc6a3, 0xFFB383, 0xAA6E56, 0xFFFFFF, 0xff7550, 0x00AAFF, 0xFFFFFF]; } // Peachy Orange AMOLED
        if(theme == 6 ) { return [0x000000, 0xffffff, 0x550000, 0xc00003, 0xFFFFFF, 0x550000, 0xFF0000, 0xffffff, 0xAA0000, 0xFF0000, 0xAA0000, 0x00AAFF, 0xFFFFFF]; } // Red and White AMOLED
        if(theme == 7 ) { return [0x000000, 0xffffff, 0x14264b, 0xaecaff, 0xFFFFFF, 0x152a53, 0x1d81e6, 0xffffff, 0x0055AA, 0x55AAFF, 0xFFAA00, 0x55AAFF, 0xFFFFFF]; } // White on Blue AMOLED
        if(theme == 8 ) { return [0x000000, 0xff960c, 0x0f2d34, 0xffbf65, 0xd5ffff, 0x0D333C, 0x61c6c6, 0xffb759, 0x9a784d, 0xa8d6fd, 0xfdb500, 0xa8d6fd, 0xe3efd2]; } // Orange on Teal AMOLED
        if(theme == 9 ) { return [0x000000, 0xffffff, 0x572d07, 0xffd6ae, 0xFFFFFF, 0x58250b, 0xf76821, 0xffffff, 0xAA5500, 0x00AAFF, 0xFFAA00, 0x00AAFF, 0xFFFFFF]; } // White and Orange AMOLED
        if(theme == 10) { return [0x000000, 0x0855ff, 0x152445, 0x4580ff, 0xb0c9ff, 0x152445, 0x4b84ff, 0x8aafff, 0x3159af, 0x55AAFF, 0xFFAA00, 0x55AAFF, 0xFFFFFF]; } // Blue AMOLED
        if(theme == 11) { return [0x000000, 0xff7600, 0x333333, 0xff9133, 0xFFFFFF, 0x333333, 0xFFAA00, 0xffffff, 0x9a9a9a, 0x55AAFF, 0xFFAA00, 0x55AAFF, 0xFFFFFF]; } // Orange AMOLED
        if(theme == 12) { return [0x000000, 0xFFFFFF, 0x333333, 0xcbcbcb, 0xFFFFFF, 0x333333, 0xFFFFFF, 0xFFFFFF, 0x9a9a9a, 0x55AAFF, 0xFFAA00, 0x55AAFF, 0xFFFFFF]; } // White on black AMOLED
        if(theme == 13) { return [0xFFFFFF, 0x000000, 0xCCCCCC, 0x666666, 0x000000, 0xCCCCCC, 0x000000, 0x000000, 0x9a9a9a, 0x000000, 0xFFAA00, 0x55AAFF, 0x555555]; } // Black on White AMOLED
        if(theme == 14) { return [0xFFFFFF, 0xAA0000, 0xCCCCCC, 0xaa2325, 0x000000, 0xCCCCCC, 0xAA0000, 0x000000, 0x9a9a9a, 0x000000, 0xFFAA00, 0x55AAFF, 0x555555]; } // Red on White AMOLED
        if(theme == 15) { return [0xFFFFFF, 0x0050ff, 0xCCCCCC, 0x2222aa, 0x000000, 0xCCCCCC, 0x0000AA, 0x000000, 0x9a9a9a, 0x000000, 0xFFAA00, 0x55AAFF, 0x555555]; } // Blue on White AMOLED
        if(theme == 16) { return [0xFFFFFF, 0x00AA00, 0xCCCCCC, 0x22aa22, 0x000000, 0xCCCCCC, 0x00AA00, 0x000000, 0x9a9a9a, 0x000000, 0xFFAA00, 0x55AAFF, 0x555555]; } // Green on White AMOLED
        if(theme == 17) { return [0xFFFFFF, 0xFF5500, 0xCCCCCC, 0xff7632, 0x000000, 0xCCCCCC, 0x555555, 0x000000, 0x9a9a9a, 0x000000, 0xFF5500, 0x55AAFF, 0x555555]; } // Orange on White AMOLED
        if(theme == 18) { return [0x000000, 0xff7700, 0x102714, 0xE64322, 0x47b047, 0x17291a, 0xff7733, 0x60d060, 0x5F9956, 0x5eff5e, 0xFF7600, 0x59B9FE, 0xFFFFFF]; } // Green and Orange AMOLED
        if(theme == 19) { return [0x000000, 0x8c9f58, 0x152B19, 0x919F6B, 0x67ab55, 0x152B19, 0xb5b872, 0x889F4A, 0x7A9A4E, 0x00FF55, 0x889F4A, 0x55AA55, 0xE3EFD2]; } // Green Camo AMOLED
        if(theme == 20) { return [0x000000, 0xFF0000, 0x282828, 0xff3236, 0xFFFFFF, 0x282828, 0xff4646, 0xFFFFFF, 0x9a9a9a, 0x55AAFF, 0xFF5555, 0x55AAFF, 0xFFFFFF]; } // Red on Black AMOLED
        if(theme == 21) { return [0xFFFFFF, 0xAA00FF, 0xCCCCCC, 0xbb34ff, 0x000000, 0xCCCCCC, 0xAA00FF, 0x000000, 0x9a9a9a, 0x000000, 0xFF5500, 0x55AAFF, 0x555555]; } // Purple on White AMOLED
        if(theme == 22) { return [0x000000, 0xAA55AA, 0x212121, 0xAA77AA, 0xffd8ff, 0x282828, 0xde79de, 0xf1b2f1, 0x9A9A9A, 0x55AAFF, 0xFFAA00, 0x55AAFF, 0xFFFFFF]; } // Purple on black AMOLED
        if(theme == 23) { return [0x000000, 0xff960c, 0x302b24, 0xffbf65, 0xffdeb4, 0x302b24, 0xffac3f, 0xffb759, 0x9a784d, 0xa8d6fd, 0xfdb500, 0xa8d6fd, 0xe3efd2]; } // Amber AMOLED
        if(theme == 30) { return parseThemeString(propColorOverride); }
        if(theme == 31) { return parseThemeString(propColorOverride2); }
        infoMessage = "THEME ERROR";
        return [0xff0000, 0x00ff00, 0x0000ff, 0xff00ff, 0x00ffff, 0xffff00, 0x550000, 0x005500, 0x000055, 0x005555, 0x550055, 0x555500, 0xffffff];
    }

    hidden function parseThemeString(override as String) as Array<Graphics.ColorType>{
        if(override.length() == 0) { return setColorTheme(-1); }
        var ret = [];
        var color_str = "";
        var color = null;
        for(var i=0; i<override.length(); i += 8) {
            color_str = override.substring(i+1, i+7);
            color = color_str.toNumberWithBase(16) as Graphics.ColorType;
            ret.add(color);
        }

        if(ret.size() != 13) {
            ret = setColorTheme(-1);
        }

        for(var j=0; j<ret.size(); j++) {
            if(ret[j] == null or ret[j] < 0 or ret[j] > 16777215) {
                ret = setColorTheme(-1);
                break;
            }
        }

        return ret;
    }

    hidden function updateColorTheme() {
        var newValue = getNightModeValue();
        if(nightModeOverride == 0) { newValue = false; }
        if(nightModeOverride == 1) { newValue = true; }

        if(nightMode != newValue) {
            if(newValue == true and propNightTheme != -1) {
                themeColors = setColorTheme(propNightTheme);
            } else {
                themeColors = setColorTheme(propTheme);
            }
            nightMode = newValue;
        }
    }

    hidden function getNightModeValue() as Boolean {
        if (propNightTheme == -1 || propNightTheme == propTheme) {
            return false;
        }

        var now = Time.now(); // Moment
        var todayMidnight = Time.today(); // Moment
        var nowAsTimeSinceMidnight = now.subtract(todayMidnight) as Duration; // Duration

        if(propNightThemeActivation == 0 or propNightThemeActivation == 1) {
            var profile = UserProfile.getProfile();
            var wakeTime = profile.wakeTime;
            var sleepTime = profile.sleepTime;

            if (wakeTime == null || sleepTime == null) {
                return false;
            }

            if(propNightThemeActivation == 1) {
                // Start two hours before sleep time
                var twoHours = new Time.Duration(7200);
                sleepTime = sleepTime.subtract(twoHours);
            }

            if(sleepTime.greaterThan(wakeTime)) {
                return (nowAsTimeSinceMidnight.greaterThan(sleepTime) || nowAsTimeSinceMidnight.lessThan(wakeTime));
            } else {
                return (nowAsTimeSinceMidnight.greaterThan(sleepTime) and nowAsTimeSinceMidnight.lessThan(wakeTime));
            }
        }

        // From Sunset to Sunrise
        if(weatherCondition != null) {
            var nextSunEventArray = getNextSunEvent();
            if(nextSunEventArray != null && nextSunEventArray.size() == 2) { 
                return nextSunEventArray[1] as Boolean;
            }
        }

        return false;
    }

    hidden function updateProperties() as Void {
        var p = Application.Properties;
        propTheme = p.getValue("colorTheme") as Number;
        propNightTheme = p.getValue("nightColorTheme") as Number;
        propNightThemeActivation = p.getValue("nightThemeActivation") as Number;
        propColorOverride = p.getValue("colorOverride") as String;
        propColorOverride2 = p.getValue("colorOverride2") as String;
        propClockOutlineStyle = p.getValue("clockOutlineStyle") as Number;
        propClockGradientOverlay = p.getValue("clockGradientOverlay") as Number;
        propClockFont = p.getValue("clockFont") as Number;
        propFontSize = p.getValue("fontSize") as Number;
        propTopPartShows = p.getValue("topPartShows") as Number;
        propHistogramData = p.getValue("histogramData") as Number;
        propHistogramSize = p.getValue("histogramSize") as Number;
        propSunriseFieldShows = p.getValue("sunriseFieldShows") as Number;
        propSunsetFieldShows = p.getValue("sunsetFieldShows") as Number;
        propWeatherLine1Shows = p.getValue("weatherLine1Shows") as Number;
        propWeatherLine2Shows = p.getValue("weatherLine2Shows") as Number;
        propWeatherFormat1 = p.getValue("weatherFormat1") as String;
        propWeatherFormat2 = p.getValue("weatherFormat2") as String;
        propDateFieldShows = p.getValue("dateFieldShows") as Number;
        propShowSeconds = p.getValue("showSeconds") as Boolean;
        propAlwaysShowSeconds = p.getValue("alwaysShowSeconds") as Boolean;
        propFieldLayout = p.getValue("fieldLayout") as Number;
        propLeftValueShows = p.getValue("leftValueShows") as Number;
        propMiddleValueShows = p.getValue("middleValueShows") as Number;
        propRightValueShows = p.getValue("rightValueShows") as Number;
        propFourthValueShows = p.getValue("fourthValueShows") as Number;
        propBottomFieldShows = p.getValue("bottomFieldShows") as Number;
        loadBottomField2Property();
        propLeftBarShows = p.getValue("leftBarShows") as Number;
        propRightBarShows = p.getValue("rightBarShows") as Number;
        propIcon1 = p.getValue("icon1") as Number;
        propIcon2 = p.getValue("icon2") as Number;
        propBatteryVariant = p.getValue("batteryVariant") as Number;

        propUpdateFreq = p.getValue("updateFreq") as Number;
        propShowClockBg = p.getValue("showClockBg") as Boolean;
        propShowDataBg = p.getValue("showDataBg") as Boolean;
        propAodStyle = p.getValue("aodStyle") as Number;
        propAodFieldShows = p.getValue("aodFieldShows") as Number;
        propAodRightFieldShows = p.getValue("aodRightFieldShows") as Number;
        propAodAlignment = p.getValue("aodAlignment") as Number;
        propDateAlignment = p.getValue("dateAlignment") as Number;
        propBottomFieldAlignment = p.getValue("bottomFieldAlignment") as Number;
        propBottomFieldLabelAlignment = p.getValue("bottomFieldLabelAlignment") as Number;
        propHemisphere = p.getValue("hemisphere") as Number;
        propHourFormat = p.getValue("hourFormat") as Number;
        propZeropadHour = p.getValue("zeropadHour") as Boolean;
        propIs24H = System.getDeviceSettings().is24Hour;
        propTimeSeparator = p.getValue("timeSeparator") as Number;
        // propTimeSeparator Auto (4): if 12h time use AM/PM (3), if 24h time use : (4)
        if (propTimeSeparator == 4) {
            if ((!propIs24H and propHourFormat == 0) or propHourFormat == 2) { propTimeSeparator = 3; } else { propTimeSeparator = 0; }
        }
        propTempUnit = p.getValue("tempUnit") as Number;
        propShowTempUnit = p.getValue("showTempUnit") as Boolean;
        propDistanceUnit = p.getValue("distanceUnit") as Number;
        propIsMetricDistance = (System.getDeviceSettings().distanceUnits == System.UNIT_METRIC and propDistanceUnit == 0) or propDistanceUnit == 1;
        propWindUnit = p.getValue("windUnit") as Number;
        propPressureUnit = p.getValue("pressureUnit") as Number;
        propLabelVisibility = p.getValue("labelVisibility") as Number;
        propDateFormat = p.getValue("dateFormat") as Number;
        propDateCustomFormat = p.getValue("dateCustomFormat") as String;
        propNotificationCountShows = p.getValue("notificationCountShows") as Number;
        propSecondsShows = p.getValue("secondsShows") as Number;
        propTzOffset1 = p.getValue("tzOffset1") as Number;
        propTzOffset2 = p.getValue("tzOffset2") as Number;
        propTzName1 = p.getValue("tzName1") as String;
        propTzName2 = p.getValue("tzName2") as String;
        propWeekOffset = p.getValue("weekOffset") as Number;
        propSmallFontVariant = p.getValue("smallFontVariant") as Number;
        propBottomFontVariant = p.getValue("bottomFontVariant") as Number;
        propStressDynamicColor = p.getValue("stressDynamicColor") as Boolean;
        propWeatherProvider = p.getValue("weatherProvider") as Number;

        nightMode = null; // force update color theme
        updateColorTheme();
        updateActiveLabels();

        if(propTimeSeparator == 2) {
            clockBgText = "####";
        } else if(propTimeSeparator == 3) {
            clockBgText = "####B";
        } else {
            if(propClockFont == 2) {
                clockBgText = "## ##";
            } else {
                clockBgText = "#####";
            }
        }
    }

    hidden function getAltitudeValue() as Float? {
        try {
            var comp = Complications.getComplication(new Id(Complications.COMPLICATION_TYPE_ALTITUDE));
            if (comp != null && comp.value != null) { return comp.value.toFloat(); }
        } catch(e) {}
        return null;
    }

    hidden function getValueForSeconds(now as Gregorian.Info) as String {
        if(propSecondsShows == -3) {
            // updateSeconds logic
            if(isSleeping and (!propAlwaysShowSeconds or canBurnIn)) {
                return "";
            } else {
                return now.sec.format("%02d");
            }
        }
        return getValueByType(propSecondsShows, 5);
    }

    hidden function getClockData(now as Gregorian.Info) as String {
        var separator = ":";
        var after = "";
        if(propTimeSeparator == 1) { separator = " "; }
        if(propTimeSeparator == 2) { separator = ""; }
        if(propTimeSeparator == 3) {
            separator = ""; 
            if(now.hour >= 12) { after = "P"; } else { after = "A"; }
        }

        if(propZeropadHour) {
            return formatHour(now.hour).format("%02d") + separator + now.min.format("%02d") + after;
        } else {
            return formatHour(now.hour).format("%2d") + separator + now.min.format("%02d") + after;
        }
    }

    hidden function getIconState(setting as Number) as String {
        if(setting == 1) { // Alarm
            var alarms = System.getDeviceSettings().alarmCount;
            if(alarms > 0) {
                return "A";
            } else {
                return "";
            }
        } else if(setting == 2) { // DND
            var dnd = System.getDeviceSettings().doNotDisturb;
            if(dnd) {
                return "D";
            } else {
                return "";
            }
        } else if(setting == 3) { // Bluetooth (on / off)
            var bl = System.getDeviceSettings().phoneConnected;
            if(bl) {
                return "L";
            } else {
                return "M";
            }
        } else if(setting == 4) { // Bluetooth (just off)
            var bl = System.getDeviceSettings().phoneConnected;
            if(bl) {
                return "";
            } else {
                return "M";
            }
        } else if(setting == 5) { // Move bar
            var mov = 0;
            if(ActivityMonitor.getInfo().moveBarLevel != null) {
                mov = ActivityMonitor.getInfo().moveBarLevel;
            }
            if(mov == 0) { return ""; }
            if(mov == 1) { return "N"; }
            if(mov == 2) { return "O"; }
            if(mov == 3) { return "P"; }
            if(mov == 4) { return "Q"; }
            if(mov == 5) { return "R"; }
        }
        return "";
    }

    hidden function getBarData(data_source as Number) as Number? {
        if(data_source == 1) {
            return getStressData();
        } else if (data_source == 2) {
            return getBBData();
        } else if (data_source == 3) {
            return getStepGoalProgress();
        } else if (data_source == 4) {
            return getFloorGoalProgress();
        } else if (data_source == 5) {
            return getActMinGoalProgress();
        } else if (data_source == 6) {
            return getMoveBar();
        }
        return null;
    }

    hidden function getStressData() as Number? {
        try {
            var complication_stress = Complications.getComplication(new Id(Complications.COMPLICATION_TYPE_STRESS));
            if (complication_stress != null && complication_stress.value != null) {
                return complication_stress.value;
            }
        } catch(e) {}
        return null;
    }

    hidden function getStressColor(val as Number) as Graphics.ColorType {
        if (val <= 25) { return 0x00AAFF; } // Rest (Blue)
        if (val <= 50) { return 0xFFAA00; } // Low (Yellow/Orange)
        if (val <= 75) { return 0xFF5500; } // Medium (Orange)
        return 0xAA0000;                   // High (Red)
    }

    hidden function getBBData() as Number? {
        try {
            var complication_bb = Complications.getComplication(new Id(Complications.COMPLICATION_TYPE_BODY_BATTERY));
            if (complication_bb != null && complication_bb.value != null) { return complication_bb.value; }
        } catch(e) {}
        return null;
    }

    hidden function goalPercent(val as Number, goal as Number) as Number {
        if(goal == 0 || val == 0) { return 0; }
        return Math.round(val.toFloat() / goal.toFloat() * 100.0);
    }

    hidden function getStepGoalProgress() as Number? {
        var info = ActivityMonitor.getInfo();
        if(info.steps != null and info.stepGoal != null) {
            return goalPercent(info.steps, info.stepGoal);
        }
        return null;
    }

    hidden function getFloorGoalProgress() as Number? {
        var info = ActivityMonitor.getInfo();
        if(info.floorsClimbed != null and info.floorsClimbedGoal != null) {
            return goalPercent(info.floorsClimbed, info.floorsClimbedGoal);
        }
        return null;
    }

    hidden function getActMinGoalProgress() as Number? {
        var info = ActivityMonitor.getInfo();
        if(info.activeMinutesWeek != null and info.activeMinutesWeekGoal != null) {
            return goalPercent(info.activeMinutesWeek.total, info.activeMinutesWeekGoal);
        }
        return null;
    }

    hidden function getMoveBar() as Number? {
        if(ActivityMonitor.getInfo().moveBarLevel != null) {
            var mov = ActivityMonitor.getInfo().moveBarLevel;
            if(mov >= 1 && mov <= 5) { return 25 + mov * 15; }
        }
        return null;
    }

    hidden function getBattData() as String {
        var value = "";

        if(propBatteryVariant == 0) {
            if (System.getSystemStats().batteryInDays != null){
                var sample = Math.round(System.getSystemStats().batteryInDays);
                value = sample.format("%0d") + "D";
            }
        }
        if(propBatteryVariant == 1) {
            var sample = System.getSystemStats().battery;
            if(sample < 100) {
                value = sample.format("%d") + "%";
            } else {
                value = sample.format("%d");
            }
        } else if(propBatteryVariant == 3 or propBatteryVariant == -1) {
                var sample = 0;
                var max = 0;
                var batLevel = System.getSystemStats().battery; 

                if(screenHeight > 280 and propFontSize == 1) {
                    // led_lines font: 2px/char, "T" outline interior → max = 24
                    sample = Math.round(batLevel / 100.0 * 24).toNumber();
                    max = 24;
                } else if(screenHeight > 280 or propFontSize == 1) {
                    // led_small_lines or smol font: 1px/char → max = 35
                    sample = Math.round(batLevel / 100.0 * 35).toNumber();
                    max = 35;
                } else {
                    sample = Math.round(batLevel / 100.0 * 20).toNumber();
                    max = 20;
                }
                if (sample > 0) {
                    value += battFull.substring(0, sample);
                }

                if (sample < max) {
                    value += battEmpty.substring(0, max - sample);
                }
            }

        return value;
    }

    hidden function formatHour(hour as Number) as Number {
        if((!propIs24H and propHourFormat == 0) or propHourFormat == 2) {
            hour = hour % 12;
            if(hour == 0) { hour = 12; }
        }
        return hour;
    }

    hidden function updateWeather() as Void {
        if (propWeatherProvider == 1) {
            // OWM provider: background service delegate handles fetching.
            // The view only reads the results from Application.Storage.
            owmError = Application.Storage.getValue("owm_error") as String?;
            try { weatherCondition = readWeatherData(); } catch(e) {}
        } else {
            // Garmin provider: original behavior unchanged.
            owmError = null;
            if (Weather.getCurrentConditions() != null) {
                weatherCondition = Weather.getCurrentConditions();
                try { storeWeatherData(); } catch(e) {}
            } else {
                try { weatherCondition = readWeatherData(); } catch(e) {}
            }
        }
        cachedTempUnit = getTempUnit();
    }

    hidden function computeCcHash(cc) as Number {
        if (cc == null) { return 0; }
        var h = 17;
        var t = (cc.temperature != null) ? cc.temperature : -127;
        h = 31 * h + t;
        var c = (cc.condition != null) ? cc.condition : -1;
        h = 31 * h + c;
        var w = (cc.windSpeed != null) ? cc.windSpeed.toNumber() : -1;
        h = 31 * h + w;
        var b = (cc.windBearing != null) ? cc.windBearing : -1;
        h = 31 * h + b;

        return h;
    }

    hidden function storeWeatherData() as Void {
        var now = Time.now().value();
        var sysStats = System.getSystemStats();

        if (!isLowMem && sysStats.freeMemory < 15000) {
            isLowMem = true;
            Application.Storage.setValue("hourly_forecast", []); 
            lastHfTime = null; 
        } else if (isLowMem && sysStats.freeMemory > 17000) {
            isLowMem = false;
        }

        var cc = Weather.getCurrentConditions();
        var newCcHash = computeCcHash(cc);

        if (lastCcHash == null || lastCcHash != newCcHash) {
            var cc_data = {};
            if(cc != null) {
                if(cc.observationLocationPosition != null) {
                    cc_data["observationLocationPosition"] = cc.observationLocationPosition.toDegrees();
                }
                if(cc.condition != null) { cc_data["condition"] = cc.condition; }
                if(cc.highTemperature != null) { cc_data["highTemperature"] = cc.highTemperature; }
                if(cc.lowTemperature != null) { cc_data["lowTemperature"] = cc.lowTemperature; }
                if(cc.precipitationChance != null) { cc_data["precipitationChance"] = cc.precipitationChance; }
                if(cc.relativeHumidity != null) { cc_data["relativeHumidity"] = cc.relativeHumidity; }
                if(cc.temperature != null) { cc_data["temperature"] = cc.temperature; }
                if(cc.feelsLikeTemperature != null) { cc_data["feelsLikeTemperature"] = cc.feelsLikeTemperature; }
                if(cc.windBearing != null) { cc_data["windBearing"] = cc.windBearing; }
                if(cc.windSpeed != null) { cc_data["windSpeed"] = cc.windSpeed; }
                if(cc has :uvIndex and cc.uvIndex != null) { cc_data["uvIndex"] = cc.uvIndex; }
            }

            cc_data["timestamp"] = now;
            Application.Storage.setValue("current_conditions", cc_data);
            
            lastCcHash = newCcHash;
        }

        if (isLowMem) { return; }

        var hf = Weather.getHourlyForecast();
        
        if (hf == null || hf.size() == 0) { return; }

        var firstForecastTime = hf[0].forecastTime.value();

        if (lastHfTime == null || lastHfTime != firstForecastTime) {
            var hf_data = [];
            
            for(var i=0; i<hf.size(); i++) {
                var tmp = {
                    "forecastTime" => hf[i].forecastTime.value(),
                    "condition" => hf[i].condition,
                    "precipitationChance" => hf[i].precipitationChance,
                    "temperature" => hf[i].temperature,
                    "windBearing" => hf[i].windBearing,
                    "windSpeed" => hf[i].windSpeed
                };
                if(hf[i] has :uvIndex) { tmp["uvIndex"] = hf[i].uvIndex; }
                
                hf_data.add(tmp);
            }

            Application.Storage.setValue("hourly_forecast", hf_data);
            lastHfTime = firstForecastTime;
        }
    }

    hidden function readWeatherData() as StoredWeather {
        var ret = new StoredWeather();
        var now = Time.now().value();
        var cc_data = Application.Storage.getValue("current_conditions") as Dictionary<String, Application.PropertyValueType>?;
        if(cc_data == null) { return ret; }
        
        var data_age_s = now - (cc_data.get("timestamp") as Number);
        var pos = cc_data.get("observationLocationPosition") as Array?;
        if (pos != null) {
            ret.observationLocationPosition = new Position.Location({:latitude => pos[0], :longitude => pos[1], :format => :degrees});
        }
        var hf_data = Application.Storage.getValue("hourly_forecast") as Array?;
        // Current conditions are valid for at least 1 hour (Garmin path), or until the
        // first forecast slot starts — whichever is later. For OWM the first slot is always
        // in the future (~3h boundary), so this covers the gap without regressing Garmin.
        var ccTimestamp = cc_data.get("timestamp") as Number;
        var ccValidUntil = ccTimestamp + 3600;
        if (hf_data != null && hf_data.size() > 0) {
            var firstForecastTime = hf_data[0].get("forecastTime") as Number;
            if (firstForecastTime > ccValidUntil) { ccValidUntil = firstForecastTime; }
        }
        if(data_age_s > 0 and now < ccValidUntil) {
            ret.condition = cc_data.get("condition") as Number;
            ret.highTemperature = cc_data.get("highTemperature") as Number;
            ret.lowTemperature = cc_data.get("lowTemperature") as Number;
            ret.precipitationChance = cc_data.get("precipitationChance") as Number;
            ret.relativeHumidity = cc_data.get("relativeHumidity") as Number;
            ret.temperature = cc_data.get("temperature") as Number;
            ret.feelsLikeTemperature = cc_data.get("feelsLikeTemperature") as Float;
            ret.windBearing = cc_data.get("windBearing") as Number;
            ret.windSpeed = cc_data.get("windSpeed") as Float;
            ret.uvIndex = cc_data.get("uvIndex") as Float;
            ret.cityName = cc_data.get("cityName") as String?;
        } else {
            if(hf_data == null) { return ret; }
            // Find the most recently passed slot. When now >= firstForecastTime there is
            // always at least one past entry, so no nearest-future fallback is needed.
            var bestEntry = null;
            var bestAge = 86401;
            for(var i=0; i<hf_data.size(); i++) {
                var forecast_age = now - (hf_data[i].get("forecastTime") as Number);
                if(forecast_age >= 0 and forecast_age < bestAge) {
                    bestAge = forecast_age;
                    bestEntry = hf_data[i];
                }
            }
            if(bestEntry != null) {
                ret.condition = bestEntry.get("condition") as Number;
                ret.temperature = bestEntry.get("temperature") as Number;
                ret.precipitationChance = bestEntry.get("precipitationChance") as Number;
                ret.windBearing = bestEntry.get("windBearing") as Number;
                ret.windSpeed = bestEntry.get("windSpeed") as Float;
                ret.uvIndex = cc_data.get("uvIndex") as Float;
            }
        }
        
        return ret;
    }

    hidden function getRecoveryTimeVal(numberFormat as String) as String {
        var complication = Complications.getComplication(new Id(Complications.COMPLICATION_TYPE_RECOVERY_TIME));
        if (complication != null && complication.value != null) {
            var recovery_h = complication.value / 60.0;
            if(recovery_h < 9.9 and recovery_h != 0) { return recovery_h.format("%.1f"); }
            return Math.round(recovery_h).format(numberFormat);
        }
        return "";
    }

    hidden function getTrainingStatusVal() as String {
        try {
            var complication = Complications.getComplication(new Id(Complications.COMPLICATION_TYPE_TRAINING_STATUS));
            if (complication != null && complication.value != null) { return complication.value.toUpper(); }
        } catch(e) {}
        return "";
    }

    hidden function getCalendarEventVal(width as Number) as String {
        var complication = Complications.getComplication(new Id(Complications.COMPLICATION_TYPE_CALENDAR_EVENTS));
        var colon_index = null;
        var val = "";
        if (complication != null && complication.value != null) {
            val = complication.value;
            colon_index = val.find(":");
            if (colon_index != null && colon_index < 2) { val = "0" + val; }
        } else {
            val = "--:--";
        }
        if (width < 5 and colon_index != null) { val = val.substring(0, 2) + val.substring(3, 5); }
        return val;
    }

    hidden function getPulseOxVal(numberFormat as String) as String {
        var complication = Complications.getComplication(new Id(Complications.COMPLICATION_TYPE_PULSE_OX));
        if (complication != null && complication.value != null) { return complication.value.format(numberFormat); }
        return "";
    }

    hidden function getValueByTypeWithUnit(complicationType as Number, width as Number) as String {
        var unit = getUnitByType(complicationType);
        if (unit.length() > 0) {
            unit = " " + unit;
        }
        return getValueByType(complicationType, width) + unit;
    }

    hidden function getUnitByType(complicationType) as String {
        var unit = "";
        if(complicationType == 10) { // Calories / day
            unit = Application.loadResource(Rez.Strings.UNIT_KCAL);
        } else if(complicationType == 11) { // Altitude (m)
            unit = Application.loadResource(Rez.Strings.UNIT_M);
        } else if(complicationType == 14) { // Altitude (ft)
            unit = Application.loadResource(Rez.Strings.UNIT_FT);
        } else if(complicationType == 16) { // Steps / day
            unit = Application.loadResource(Rez.Strings.UNIT_STEPS);
        } else if(complicationType == 18) { // Wheelchair pushes
            unit = Application.loadResource(Rez.Strings.UNIT_PUSHES);
        } else if(complicationType == 25) { // Active calories / day
            unit = Application.loadResource(Rez.Strings.UNIT_KCAL);
        } else if(complicationType == 46) { // Active/Total calories / day
            unit = Application.loadResource(Rez.Strings.UNIT_KCAL);
        }
        return unit;
    }

    hidden function getValueByType(complicationType as Number, width as Number) as String {
        var val = "";
        var numberFormat = "%d";
        var activityInfo = null;

        if(complicationType == -2) { // Hidden
            return "";
        } else if(complicationType == -1) { // Date
            val = formatDate();
        } else if(complicationType == 0) { // Active min / week
            activityInfo = ActivityMonitor.getInfo();
            if(activityInfo.activeMinutesWeek != null) {
                val = activityInfo.activeMinutesWeek.total.format(numberFormat);
            }
        } else if(complicationType == 62) { // Active hours / week
            if(activityInfo == null) { activityInfo = ActivityMonitor.getInfo(); }
            if(activityInfo.activeMinutesWeek != null) {
                val = (activityInfo.activeMinutesWeek.total / 60.0).format("%.1f");
            }
        } else if(complicationType == 1) { // Active min / day
            if(activityInfo == null) { activityInfo = ActivityMonitor.getInfo(); }
            if(activityInfo.activeMinutesDay != null) {
                val = activityInfo.activeMinutesDay.total.format(numberFormat);
            }
        } else if(complicationType == 2) { // distance / day
            if(activityInfo == null) { activityInfo = ActivityMonitor.getInfo(); }
            if(activityInfo.distance != null) {
                val = formatDistanceByWidth(activityInfo.distance / (propIsMetricDistance ? 100000.0 : 160900.0), width);
            }
        } else if(complicationType == 3) { // floors climbed / day
            if(activityInfo == null) { activityInfo = ActivityMonitor.getInfo(); }
            if(activityInfo.floorsClimbed != null) {
                val = activityInfo.floorsClimbed.format(numberFormat);
            }
        } else if(complicationType == 4) { // meters climbed / day
            if(activityInfo == null) { activityInfo = ActivityMonitor.getInfo(); }
            if(activityInfo.metersClimbed != null) {
                val = activityInfo.metersClimbed.format(numberFormat);
            }
        } else if(complicationType == 5) { // Time to Recovery (h)
            val = getRecoveryTimeVal(numberFormat);

        } else if(complicationType == 6) { // VO2 Max Running
            var profile = UserProfile.getProfile();
            if(profile.vo2maxRunning != null) {
                val = profile.vo2maxRunning.format(numberFormat);
            }
        } else if(complicationType == 7) { // VO2 Max Cycling
            var profile = UserProfile.getProfile();
            if(profile.vo2maxCycling != null) {
                val = profile.vo2maxCycling.format(numberFormat);
            }
        } else if(complicationType == 8) { // Respiration rate
            if(activityInfo == null) { activityInfo = ActivityMonitor.getInfo(); }
            if(activityInfo.respirationRate != null) {
                val = activityInfo.respirationRate.format(numberFormat);
            }
        } else if(complicationType == 9) {
            // Try to retrieve live HR from Activity::Info
            var activity_info = Activity.getActivityInfo();
            var sample = activity_info.currentHeartRate;
            if(sample != null) {
                val = sample.format("%01d");
            } else {
                var history = ActivityMonitor.getHeartRateHistory(1, /* newestFirst */ true);
                if (history != null) {
                    var hist = history.next();
                    if ((hist != null) && (hist.heartRate != ActivityMonitor.INVALID_HR_SAMPLE)) {
                        val = hist.heartRate.format("%01d");
                    }
                }
            }
        } else if(complicationType == 10) { // Calories
            if(activityInfo == null) { activityInfo = ActivityMonitor.getInfo(); }
            if(activityInfo.calories != null) {
                val = activityInfo.calories.format(numberFormat);
            }
        } else if(complicationType == 11) { // Altitude (m)
                var alt = getAltitudeValue();
                if (alt != null) {
                    val = alt.format(numberFormat);
            }
        } else if(complicationType == 12) { // Stress
        var st = getStressData();
            if(st != null) {
                val = st.format(numberFormat);
            }
        } else if(complicationType == 13) { // Body battery
            var bb = getBBData();
            if(bb != null) {
                val = bb.format(numberFormat);
            }
        } else if(complicationType == 14) { // Altitude (ft)
            var alt = getAltitudeValue();
            if (alt != null) {
                val = (alt * 3.28084).format(numberFormat);
            }
        } else if(complicationType == 15) { // Alt TZ 1
            val = secondaryTimezone(propTzOffset1, width);
        } else if(complicationType == 16) { // Steps / day
            if(activityInfo == null) { activityInfo = ActivityMonitor.getInfo(); }
            if(activityInfo.steps != null) {
                var steps = activityInfo.steps;
                if(width >= 5) {
                    val = steps.format("%d");
                } else if(width == 4 and steps < 10000) {
                    val = steps.format("%d");
                } else {
                    val = (steps / 1000).format("%d") + "K";
                }
            }
        } else if(complicationType == 17) { // Distance (m) / day
            if(activityInfo == null) { activityInfo = ActivityMonitor.getInfo(); }
            if(activityInfo.distance != null) {
                val = (activityInfo.distance / 100).format(numberFormat);
            }
        } else if(complicationType == 18) { // Wheelchair pushes
            if(activityInfo == null) { activityInfo = ActivityMonitor.getInfo(); }
            if(activityInfo.pushes != null) {
                val = activityInfo.pushes.format(numberFormat);
            }
        } else if(complicationType == 19) { // Weekly run distance
            val = getWeeklyDistanceFromComplication(true, propIsMetricDistance ? 0.001 : 0.000621371, width);
        } else if(complicationType == 20) { // Weekly bike distance
            val = getWeeklyDistanceFromComplication(false, propIsMetricDistance ? 0.001 : 0.000621371, width);
        } else if(complicationType == 21) { // Training status
            val = getTrainingStatusVal();
        } else if(complicationType == 22) { // Raw Barometric pressure (hPA)
            var info = Activity.getActivityInfo();
            if (info.rawAmbientPressure != null) {
                val = formatPressure(info.rawAmbientPressure / 100.0, width);
            }
        } else if(complicationType == 23) { // Weight kg
            var profile = UserProfile.getProfile();
            if(profile.weight != null) {
                var weight_kg = profile.weight / 1000.0;
                if (width == 3) {
                    val = weight_kg.format(numberFormat);
                } else {
                    val = weight_kg.format("%.1f");
                }
            }
        } else if(complicationType == 24) { // Weight lbs
            var profile = UserProfile.getProfile();
            if(profile.weight != null) {
                val = (profile.weight * 0.00220462).format(numberFormat);
            }
        } else if(complicationType == 25) { // Act Calories
            if(activityInfo == null) { activityInfo = ActivityMonitor.getInfo(); }
            var rest_calories = getRestCalories();
            // Get total calories and subtract rest calories
            if (activityInfo.calories != null && rest_calories > 0) {
                var active_calories = activityInfo.calories - rest_calories;
                if (active_calories > 0) {
                    val = active_calories.format(numberFormat);
                } else { val = "0"; }
            }
        } else if(complicationType == 26) { // Sea level pressure (hPA)
            var info = Activity.getActivityInfo();
            if (info.meanSeaLevelPressure != null) {
                val = formatPressure(info.meanSeaLevelPressure / 100.0, width);
            }
        } else if(complicationType == 27) { // Week number
            var today = Time.Gregorian.info(Time.now(), Time.FORMAT_SHORT);
            var week_number = isoWeekNumber(today.year, today.month, today.day);
            val = week_number.format(numberFormat);
        } else if(complicationType == 28 || complicationType == 29) { // Total distance past 7 days
            val = formatDistanceByWidth(getWeeklyDistance() * (propIsMetricDistance ? 0.00001 : 0.00000621371), width);
        } else if(complicationType == 30) { // Battery percentage
            var battery = System.getSystemStats().battery;
            val = battery.format("%d");
        } else if(complicationType == 31) { // Battery days remaining
            var stats35 = System.getSystemStats();
            if(stats35.batteryInDays != null) {
                val = Math.round(stats35.batteryInDays).format(numberFormat);
            }
        } else if(complicationType == 32) { // Notification count
            var notif_count = System.getDeviceSettings().notificationCount;
            if(notif_count != null) {
                if(notif_count == 0) {
                    val = ""; // Hide when zero
                } else {
                    val = notif_count.format(numberFormat);
                }
            }
        } else if(complicationType == 33) { // Solar intensity
            var stats37 = System.getSystemStats();
            if(stats37.solarIntensity != null) {
                val = stats37.solarIntensity.format(numberFormat);
            }
        } else if(complicationType == 34) { // Sensor temperature
            var tempIterator = Toybox.SensorHistory.getTemperatureHistory({:period => 1});
            if (tempIterator != null) {
                var temp = tempIterator.next();
                if(temp != null and temp.data != null) {
                    val = formatTemperature(convertTemperature(temp.data, cachedTempUnit));
                }
            }
        } else if(complicationType == 35 || complicationType == 36) { // Sunrise / Sunset
            if(weatherCondition != null) {
                var loc = weatherCondition.observationLocationPosition;
                if(loc != null) {
                    var now = Time.now();
                    var s = (complicationType == 35) ? Weather.getSunrise(loc, now) : Weather.getSunset(loc, now);
                    val = formatSunTime(s, width);
                }
            }
        } else if(complicationType == 37) { // Alt TZ 2
            val = secondaryTimezone(propTzOffset2, width);
        } else if(complicationType == 38) { // Alarms
            val = System.getDeviceSettings().alarmCount.format(numberFormat);
        } else if(complicationType == 39) { // High temp
            if(weatherCondition != null and weatherCondition.highTemperature != null) {
                var tempVal = weatherCondition.highTemperature;
                val = formatTemperature(convertTemperature(tempVal, cachedTempUnit));
            }
        } else if(complicationType == 40) { // Low temp
            if(weatherCondition != null and weatherCondition.lowTemperature != null) {
                var tempVal = weatherCondition.lowTemperature;
                val = formatTemperature(convertTemperature(tempVal, cachedTempUnit));
            }
        } else if(complicationType == 41) { // Temperature
            val = getTemperature();
        } else if(complicationType == 42) { // Precipitation chance
            val = getPrecip();
            if(width == 3 and val.equals("100%")) { val = "100"; }
        } else if(complicationType == 43) { // Next Sun Event
            var nextSunEventArray = getNextSunEvent();
            if(nextSunEventArray != null && nextSunEventArray.size() == 2) {
                val = formatSunTime(nextSunEventArray[0], width);
            }
        } else if(complicationType == 44) { // Millitary Date Time Group
            val = getDateTimeGroup();
        } else if(complicationType == 45) { // Time of the next Calendar Event
            val = getCalendarEventVal(width);
        } else if(complicationType == 46) { // Active / Total calories
            if(activityInfo == null) { activityInfo = ActivityMonitor.getInfo(); }
            var rest_calories = getRestCalories();
            var total_calories = 0;
            // Get total calories and subtract rest calories
            if (activityInfo.calories != null) {
                total_calories = activityInfo.calories;
            }
            var active_calories = total_calories - rest_calories;
            active_calories = (active_calories > 0) ? active_calories : 0; // Ensure active calories is not negative
            val = active_calories.format(numberFormat) + "/" + total_calories.format(numberFormat);
        } else if(complicationType == 47) { // PulseOx
            val = getPulseOxVal(numberFormat);
        } else if(complicationType == 48) { // Location Long Lat dec deg
            var pos = Activity.getActivityInfo().currentLocation;
            if(pos != null) {
                val = pos.toDegrees()[0] + " " + pos.toDegrees()[1];
            } else {
                val = Application.loadResource(Rez.Strings.LABEL_POS_NA);
            }

        } else if(complicationType == 49) { // Location Millitary format
            var pos = Activity.getActivityInfo().currentLocation;
            if(pos != null) {
                val = pos.toGeoString(Position.GEO_MGRS);
            } else {
                val = Application.loadResource(Rez.Strings.LABEL_POS_NA);
            }

        } else if(complicationType == 50) { // Location Accuracy
            var acc = Activity.getActivityInfo().currentLocationAccuracy;
            if(acc != null) {
                if(width < 4) {
                    val = (acc as Number).format("%d");
                } else {
                    val = ["N/A", "LAST", "POOR", "USBL", "GOOD"][acc];
                }
            }
        } else if(complicationType == 51) { // UV Index
            val = getUVIndex();
        } else if(complicationType == 52) { // Humidity
            val = getHumidity();
        } else if(complicationType == 53) { // CGM Glucose + Trend
            val = getCgmReading();
        } else if(complicationType == 54) { // CGM Age (minutes)
            val = getCgmAge();
        } else if(complicationType == 55) { // Feels like
            val = getFeelsLike();
        } else if(complicationType == 56) { // Hours to next sun event
            val = hoursToNextSunEvent();
        } else if(complicationType == 57) { // Resting Heart Rate
            var profile = UserProfile.getProfile();
            if(profile.restingHeartRate != null) {
                val = profile.restingHeartRate.format(numberFormat);
            }
        } else if(complicationType == 58 || complicationType == 59) { // Run/bike distance past 7 days
            if(Time.now().value() - lastActivityDistUpdate >= 60*5) {
                lastActivityDistUpdate = Time.now().value();
                updateActivityDistCache();
            }
            var distFactor = propIsMetricDistance ? 0.001 : 0.000621371;
            val = formatDistanceByWidth((complicationType == 58 ? cachedRunDist7Days : cachedBikeDist7Days) * distFactor, width);
        } else if(complicationType == 65) { // Swim distance past 7 days
            if(Time.now().value() - lastActivityDistUpdate >= 60*5) {
                lastActivityDistUpdate = Time.now().value();
                updateActivityDistCache();
            }
            var distFactor = propIsMetricDistance ? 0.001 : 0.000621371;
            val = formatDistanceByWidth(cachedSwimDist7Days * distFactor, width);
        } else if(complicationType == 60) { // Weather data 1 format string
            val = getWeatherByFormat(propWeatherFormat1);
        } else if(complicationType == 61) { // Weather data 2 format string
            val = getWeatherByFormat(propWeatherFormat2);
        } else if(complicationType == 63 || complicationType == 64) { // Civil dawn / Civil dusk
            if(weatherCondition != null) {
                var loc = weatherCondition.observationLocationPosition;
                if(loc != null) {
                    var now = Time.now();
                    var sunrise = Weather.getSunrise(loc, now);
                    var sunset = Weather.getSunset(loc, now);
                    if(sunrise != null && sunset != null) {
                        var latDeg = loc.toDegrees()[0];
                        var twilight = getCivilTwilight(latDeg as Double, sunrise, sunset);
                        if(twilight != null) {
                            val = formatSunTime(complicationType == 63 ? twilight[0] : twilight[1], width);
                        }
                    }
                }
            }
        }

        return val;
    }

    hidden function getDataArrayByType(dataSource as Number) as Array<Number> {
        var ret = [];
        var iterator = null;
        var max = null;
        var twoHours = new Time.Duration(7200);
        
        if(dataSource == 0) {
            iterator = Toybox.SensorHistory.getBodyBatteryHistory({:period => twoHours, :order => Toybox.SensorHistory.ORDER_OLDEST_FIRST});
            max = 100;
        } else if(dataSource == 1) {
            iterator = Toybox.SensorHistory.getElevationHistory({:period => twoHours, :order => Toybox.SensorHistory.ORDER_OLDEST_FIRST});
        } else if(dataSource == 2) {
            iterator = Toybox.SensorHistory.getHeartRateHistory({:period => twoHours, :order => Toybox.SensorHistory.ORDER_OLDEST_FIRST});
        } else if(dataSource == 3) {
            iterator = Toybox.SensorHistory.getOxygenSaturationHistory({:period => twoHours, :order => Toybox.SensorHistory.ORDER_OLDEST_FIRST});
            max = 100;
        } else if(dataSource == 4) {
            iterator = Toybox.SensorHistory.getPressureHistory({:period => twoHours, :order => Toybox.SensorHistory.ORDER_OLDEST_FIRST});
        } else if(dataSource == 5 or dataSource == 7) {
            iterator = Toybox.SensorHistory.getStressHistory({:period => twoHours, :order => Toybox.SensorHistory.ORDER_OLDEST_FIRST});
            max = 100;
        } else if(dataSource == 6) {
            iterator = Toybox.SensorHistory.getTemperatureHistory({:period => twoHours, :order => Toybox.SensorHistory.ORDER_OLDEST_FIRST});
        }

        if(iterator == null) { return ret; }
        if(max == null) {
            max = iterator.getMax();
        }
        var min = iterator.getMin();
        if(min == null or max == null) {
            return ret;
        }
        var diff = max - (min * 0.9);
        var sample = iterator.next();
        var count = 0;
        while(sample != null) {
            if(dataSource == 2) {
                if(sample.data != null and sample.data != 0 and sample.data < 255) {
                    ret.add(Math.round(sample.data.toFloat() / max * 100).toNumber());
                }
            } else if(dataSource == 1 or dataSource == 4) {
                if(sample.data != null) {
                    ret.add(Math.round((sample.data.toFloat() - Math.round(min * 0.9)) / diff * 100).toNumber());
                }
            } else if(dataSource == 3) {
                if(sample.data != null) {
                    ret.add(Math.round((sample.data.toFloat() - 50.0) / 50.0 * 100).toNumber());
                }
            } else {
                if(sample.data != null) {
                    ret.add(Math.round(sample.data.toFloat() / max * 100).toNumber());
                }
            }
            
            sample = iterator.next();
            count++;
        }

        if(ret.size() > histogramTargetWidth) {
            var reduced_ret = [];
            var step = (ret.size() as Float) / histogramTargetWidth.toFloat();
            var closest_index = 0;
            for(var i=0; i<histogramTargetWidth; i++) {
                closest_index = Math.round(i * step).toNumber();
                if (closest_index >= ret.size()) {
                    closest_index = ret.size() - 1;
                }
                reduced_ret.add(ret[closest_index]);
            }
            return reduced_ret;
        }
        return ret;
    } 

    hidden function getLabelByType(complicationType as Number, labelSize as Number) as String {
        // labelSize 1 = short, 2 = mid
        if(complicationType == 15) { return propTzName1.toUpper() + ":"; }
        if(complicationType == 37) { return propTzName2.toUpper() + ":"; }
        switch(complicationType) {
            case 0: return formatLabel(Rez.Strings.LABEL_WMIN_1, Rez.Strings.LABEL_WMIN_2, labelSize);
            case 62: return formatLabel(Rez.Strings.LABEL_WHRS_1, Rez.Strings.LABEL_WHRS_2, labelSize);
            case 1: return formatLabel(Rez.Strings.LABEL_DMIN_1, Rez.Strings.LABEL_DMIN_2, labelSize);
            case 2:
                if(propIsMetricDistance) { return formatLabel(Rez.Strings.LABEL_DKM_1, Rez.Strings.LABEL_DKM_2, labelSize); }
                return formatLabel(Rez.Strings.LABEL_DMI_1, Rez.Strings.LABEL_DMI_2, labelSize);
            case 3: return Application.loadResource(Rez.Strings.LABEL_FLOORS);
            case 4: return formatLabel(Rez.Strings.LABEL_CLIMB_1, Rez.Strings.LABEL_CLIMB_2, labelSize);
            case 5: return formatLabel(Rez.Strings.LABEL_RECOV_1, Rez.Strings.LABEL_RECOV_2, labelSize);
            case 6: return formatLabel(Rez.Strings.LABEL_VO2_1, Rez.Strings.LABEL_VO2RUN_2, labelSize);
            case 7: return formatLabel(Rez.Strings.LABEL_VO2_1, Rez.Strings.LABEL_VO2BIKE_2, labelSize);
            case 8: return formatLabel(Rez.Strings.LABEL_RESP_1, Rez.Strings.LABEL_RESP_2, labelSize);
            case 9: return Application.loadResource(Rez.Strings.LABEL_HR);
            case 10: return formatLabel(Rez.Strings.LABEL_CAL_1, Rez.Strings.LABEL_CAL_2, labelSize);
            case 11: return formatLabel(Rez.Strings.LABEL_ALT_1, Rez.Strings.LABEL_ALT_2, labelSize);
            case 12: return Application.loadResource(Rez.Strings.LABEL_STRESS);
            case 13: return formatLabel(Rez.Strings.LABEL_BBAT_1, Rez.Strings.LABEL_BBAT_2, labelSize);
            case 14: return formatLabel(Rez.Strings.LABEL_ALT_1, Rez.Strings.LABEL_ALT_2, labelSize);
            case 16: return Application.loadResource(Rez.Strings.LABEL_STEPS);
            case 17: return formatLabel(Rez.Strings.LABEL_DIST_1, Rez.Strings.LABEL_DIST_2, labelSize);
            case 18: return Application.loadResource(Rez.Strings.LABEL_PUSHES);
            case 58:
            case 19:
                if(propIsMetricDistance) { return formatLabel(Rez.Strings.LABEL_WKM_1, Rez.Strings.LABEL_WRUNM_2, labelSize); }
                return formatLabel(Rez.Strings.LABEL_WMI_1, Rez.Strings.LABEL_WRUNMI_2, labelSize);
            case 59:
            case 20:
                if(propIsMetricDistance) { return formatLabel(Rez.Strings.LABEL_WKM_1, Rez.Strings.LABEL_WBIKEKM_2, labelSize); }
                return formatLabel(Rez.Strings.LABEL_WMI_1, Rez.Strings.LABEL_WBIKEMI_2, labelSize);
            case 21: return Application.loadResource(Rez.Strings.LABEL_TRAINING);
            case 22: return Application.loadResource(Rez.Strings.LABEL_PRESSURE);
            case 23: return formatLabel(Rez.Strings.LABEL_KG_1, Rez.Strings.LABEL_WEIGHT_2, labelSize);
            case 24: return formatLabel(Rez.Strings.LABEL_LBS_1, Rez.Strings.LABEL_WEIGHT_2, labelSize);
            case 25: return formatLabel(Rez.Strings.LABEL_ACAL_1, Rez.Strings.LABEL_ACAL_2, labelSize);
            case 26: return Application.loadResource(Rez.Strings.LABEL_PRESSURE);
            case 27: return Application.loadResource(Rez.Strings.LABEL_WEEK);
            case 29:
            case 28:
                if(propIsMetricDistance) { return formatLabel(Rez.Strings.LABEL_WKM_1, Rez.Strings.LABEL_WDISTKM_2, labelSize); }
                return formatLabel(Rez.Strings.LABEL_WMI_1, Rez.Strings.LABEL_WDISTMI_2, labelSize);
            case 30: return formatLabel(Rez.Strings.LABEL_BATT_1, Rez.Strings.LABEL_BATT_2, labelSize);
            case 31: return formatLabel(Rez.Strings.LABEL_BATTD_1, Rez.Strings.LABEL_BATTD_2, labelSize);
            case 32: return formatLabel(Rez.Strings.LABEL_NOTIFS_1, Rez.Strings.LABEL_NOTIFS_1, labelSize);
            case 33: return formatLabel(Rez.Strings.LABEL_SUN_1, Rez.Strings.LABEL_SUNINT_2, labelSize);
            case 34: return formatLabel(Rez.Strings.LABEL_TEMP_1, Rez.Strings.LABEL_STEMP_2, labelSize);
            case 35: return formatLabel(Rez.Strings.LABEL_SUNRISE_1, Rez.Strings.LABEL_SUNRISE_2, labelSize);
            case 36: return formatLabel(Rez.Strings.LABEL_SUNSET_1, Rez.Strings.LABEL_SUNSET_2, labelSize);
            case 38: return formatLabel(Rez.Strings.LABEL_ALARM_1, Rez.Strings.LABEL_ALARM_2, labelSize);
            case 39: return formatLabel(Rez.Strings.LABEL_HIGH_1, Rez.Strings.LABEL_HIGH_2, labelSize);
            case 40: return formatLabel(Rez.Strings.LABEL_LOW_1, Rez.Strings.LABEL_LOW_2, labelSize);
            case 41: return formatLabel(Rez.Strings.LABEL_TEMP_1, Rez.Strings.LABEL_TEMP_1, labelSize);
            case 42: return formatLabel(Rez.Strings.LABEL_PRECIP_1, Rez.Strings.LABEL_PRECIP_1, labelSize);
            case 43: return formatLabel(Rez.Strings.LABEL_NEXTSUN_1, Rez.Strings.LABEL_NEXTSUN_2, labelSize);
            case 45: return formatLabel(Rez.Strings.LABEL_NEXTCAL_1, Rez.Strings.LABEL_NEXTCAL_2, labelSize);
            case 47: return formatLabel(Rez.Strings.LABEL_OX_1, Rez.Strings.LABEL_OX_2, labelSize);
            case 50: return formatLabel(Rez.Strings.LABEL_ACC_1, Rez.Strings.LABEL_ACC_2, labelSize);
            case 51: return formatLabel(Rez.Strings.LABEL_UV_1, Rez.Strings.LABEL_UV_2, labelSize);
            case 52: return formatLabel(Rez.Strings.LABEL_HUM_1, Rez.Strings.LABEL_HUM_2, labelSize);
            case 53: return WatchUi.loadResource(Rez.Strings.LABEL_CGM) as String;
            case 54: return WatchUi.loadResource(Rez.Strings.LABEL_CGMAGE) as String;
            case 55: return formatLabel(Rez.Strings.LABEL_FL, Rez.Strings.LABEL_FL_2, labelSize);
            case 56: return formatLabel(Rez.Strings.LABEL_HRS_NEXT_SUN_EVENT_1, Rez.Strings.LABEL_HRS_NEXT_SUN_EVENT_1, labelSize);
            case 57: return formatLabel(Rez.Strings.LABEL_RHR_1, Rez.Strings.LABEL_RHR_2, labelSize);
            case 63: return Application.loadResource(Rez.Strings.LABEL_DAWN_1) as String;
            case 64: return Application.loadResource(Rez.Strings.LABEL_DUSK_1) as String;
            case 65:
                if(propIsMetricDistance) { return formatLabel(Rez.Strings.LABEL_WKM_1, Rez.Strings.LABEL_WSWIMKM_2, labelSize); }
                return formatLabel(Rez.Strings.LABEL_WMI_1, Rez.Strings.LABEL_WSWIMMI_2, labelSize);
        }
        return "";
    }

    hidden function formatLabel(short as ResourceId, mid as ResourceId, size as Number) as String {
        if(size == 1) { return Application.loadResource(short) + ":"; }
        return Application.loadResource(mid) + ":";
    }

    hidden function formatDate() as String {
        var now = Time.now();
        var today = Time.Gregorian.info(now, Time.FORMAT_SHORT);

        if(propDateFormat == 1) {
            return formatCustomDate(today);
        }
        // Auto: omit year for large font
        var base = dayName(today.day_of_week) + ", " + today.day + " " + monthName(today.month);
        if(propFontSize == 1) {
            return base;
        }
        return base + " " + today.year;
    }

    hidden function formatCustomDate(today as Time.Gregorian.Info) as String {
        var fmt = propDateCustomFormat;
        var result = "";
        var i = 0;
        while(i < fmt.length()) {
            var ch = fmt.substring(i, i + 1);
            if(ch.equals("y")) { result += today.year.toString(); }
            else if(ch.equals("m")) { result += today.month.format("%02d"); }
            else if(ch.equals("d")) { result += today.day.toString(); }
            else if(ch.equals("o")) { result += dayName(today.day_of_week); }
            else if(ch.equals("n")) { result += monthName(today.month); }
            else if(ch.equals("w")) { result += isoWeekNumber(today.year, today.month, today.day).toString(); }
            else { result += ch; }
            i += 1;
        }
        return result;
    }

    hidden function getWeatherByFormat(format as String) as String {
        var result = "";
        var i = 0;
        while(i < format.length()) {
            var ch = format.substring(i, i + 1);
            if(ch.equals("t")) { result = result + getTemperature(); }
            else if(ch.equals("w")) { result = result + getWind(); }
            else if(ch.equals("h")) { result = result + getHumidity(); }
            else if(ch.equals("p")) { result = result + getPrecip(); }
            else if(ch.equals("u")) { result = result + getUVIndex(); }
            else if(ch.equals("l")) { result = result + getHighLow(); }
            else if(ch.equals("f")) { result = result + getFeelsLike(); }
            else if(ch.equals("c")) { result = result + getWeatherCondition(); }
            else if(ch.equals("s")) { result = result + getWeatherConditionShort(); }
            else if(ch.equals("n")) { result = result + getCityName(); }
            else { result = result + ch; }
            i += 1;
        }
        return result;
    }

    hidden function getDateTimeGroup() as String {
        // 052125ZMAR25
        // DDHHMMZmmmYY
        var now = Time.now();
        var utc = Time.Gregorian.utcInfo(now, Time.FORMAT_SHORT);
        var value = utc.day.format("%02d") + utc.hour.format("%02d") + utc.min.format("%02d") + "Z" + monthName(utc.month) + utc.year.toString().substring(2,4);

        return value;
    }

    hidden function formatPressure(pressureHpa as Float, width as Number) as String {
        var val = "";
        var nf = "%d";

        if (propPressureUnit == 0) { // hPA
            val = pressureHpa.format(nf);
        } else if (propPressureUnit == 1) { // mmHG
            val = (pressureHpa * 0.750062).format(nf);
        } else if (propPressureUnit == 2) { // inHG
            if(width == 5) {
                val = (pressureHpa * 0.02953).format("%.2f");
            } else {
                val = (pressureHpa * 0.02953).format("%.1f");
            }
        }

        return val;
    }

    hidden function moonPhase(time) as String {
        var jd = julianDay(time.year, time.month, time.day);

        var days_since_new_moon = jd - 2459966;
        var lunar_cycle = 29.53;
        var phase = ((days_since_new_moon / lunar_cycle) * 100).toNumber() % 100;
        var into_cycle = (phase / 100.0) * lunar_cycle;

        if(time.month == 5 and time.day == 4) {
            return "8"; // That's no moon!
        }

        var moonPhase;
        if (into_cycle < 3) { // 2+1
            moonPhase = 0;
        } else if (into_cycle < 6) { // 4
            moonPhase = 1;
        } else if (into_cycle < 10) { // 4
            moonPhase = 2;
        } else if (into_cycle < 14) { // 4
            moonPhase = 3;
        } else if (into_cycle < 18) { // 4
            moonPhase = 4;
        } else if (into_cycle < 22) { // 4
            moonPhase = 5;
        } else if (into_cycle < 26) { // 4
            moonPhase = 6;
        } else if (into_cycle < 29) { // 3
            moonPhase = 7;
        } else {
            moonPhase = 0;
        }

        // If hemisphere is 1 (southern), invert the phase index
        if (propHemisphere == 1) {
            moonPhase = (8 - moonPhase) % 8;
        }

        return moonPhase.toString();

    }

    hidden function formatDistanceByWidth(distance as Float, width as Number) as String {
        if (width == 3) {
            return distance < 9.9 ? distance.format("%.1f") : Math.round(distance).format("%d");
        } else if (width == 4) {
            return distance < 100 ? distance.format("%.1f") : distance.format("%d");
        } else {  // width == 5
            return distance < 1000 ? distance.format("%05.1f") : distance.format("%05d");
        }
    }

    hidden function getCityName() as String {
        if (weatherCondition == null || !(weatherCondition has :cityName) || weatherCondition.cityName == null) { return ""; }
        return weatherCondition.cityName.toUpper();
    }

    hidden function getWeatherCondition() as String {
        if (owmError != null) { return owmError; }
        // Early return if no weather data
        if (weatherCondition == null || weatherCondition.condition == null) {
            return "";
        }

        var weatherStrings = [
            Rez.Strings.WEATHER_0, Rez.Strings.WEATHER_1, Rez.Strings.WEATHER_2, Rez.Strings.WEATHER_3,
            Rez.Strings.WEATHER_4, Rez.Strings.WEATHER_5, Rez.Strings.WEATHER_6, Rez.Strings.WEATHER_7,
            Rez.Strings.WEATHER_8, Rez.Strings.WEATHER_9, Rez.Strings.WEATHER_10, Rez.Strings.WEATHER_11,
            Rez.Strings.WEATHER_12, Rez.Strings.WEATHER_13, Rez.Strings.WEATHER_14, Rez.Strings.WEATHER_15,
            Rez.Strings.WEATHER_16, Rez.Strings.WEATHER_17, Rez.Strings.WEATHER_18, Rez.Strings.WEATHER_19,
            Rez.Strings.WEATHER_20, Rez.Strings.WEATHER_21, Rez.Strings.WEATHER_22, Rez.Strings.WEATHER_23,
            Rez.Strings.WEATHER_24, Rez.Strings.WEATHER_25, Rez.Strings.WEATHER_26, Rez.Strings.WEATHER_27,
            Rez.Strings.WEATHER_28, Rez.Strings.WEATHER_29, Rez.Strings.WEATHER_30, Rez.Strings.WEATHER_31,
            Rez.Strings.WEATHER_32, Rez.Strings.WEATHER_33, Rez.Strings.WEATHER_34, Rez.Strings.WEATHER_35,
            Rez.Strings.WEATHER_36, Rez.Strings.WEATHER_37, Rez.Strings.WEATHER_38, Rez.Strings.WEATHER_39,
            Rez.Strings.WEATHER_40, Rez.Strings.WEATHER_41, Rez.Strings.WEATHER_42, Rez.Strings.WEATHER_43,
            Rez.Strings.WEATHER_44, Rez.Strings.WEATHER_45, Rez.Strings.WEATHER_46, Rez.Strings.WEATHER_47,
            Rez.Strings.WEATHER_48, Rez.Strings.WEATHER_49, Rez.Strings.WEATHER_50, Rez.Strings.WEATHER_51,
            Rez.Strings.WEATHER_52, Rez.Strings.WEATHER_53
        ];
        var idx = weatherCondition.condition.toNumber();
        if (idx < 0 || idx >= weatherStrings.size()) { idx = 53; }
        return Application.loadResource(weatherStrings[idx]);
    }

    hidden function getWeatherConditionShort() as String {
        if (owmError != null) { return owmError; }
        if (weatherCondition == null || weatherCondition.condition == null) {
            return "";
        }
        var short = [
            "CLEAR", "CLOUDY", "CLOUDY", "RAIN", "SNOW", "WINDY", "THUNDER",
            "WINTRY", "FOG", "HAZY", "HAIL", "SHOWERS", "THUNDER", "UNKNOWN",
            "RAIN", "HVY RAIN", "SNOW", "HVY SNOW", "RAIN SNOW", "RAIN SNOW",
            "CLOUDY", "RAIN SNOW", "CLEAR", "CLEAR", "SHOWERS", "SHOWERS",
            "SHOWERS", "(SHOWERS)", "(THUNDER)", "MIST", "DUST", "DRIZZLE",
            "TORNADO", "SMOKE", "ICE", "SAND", "SQUALL", "SANDSTORM",
            "VOLC ASH", "HAZE", "FAIR", "HURRICANE", "TROP STORM", "(SNOW)",
            "(RAIN SNOW)", "(RAIN)", "(SNOW)", "(RAIN SNOW)", "FLURRIES",
            "FRZ RAIN", "SLEET", "ICE SNOW", "CLOUDY", "UNKNOWN"
        ];
        var idx = weatherCondition.condition.toNumber();
        if (idx < 0 || idx >= short.size()) { idx = 53; }
        return short[idx];
    }

    hidden function getTemperature() as String {
        if(weatherCondition != null and weatherCondition.temperature != null) {
            var temp_val = weatherCondition.temperature;
            return formatTemperature(convertTemperature(temp_val, cachedTempUnit));
        }
        return "";
    }

    hidden function getTempUnit() as String {
        var temp_unit_setting = System.getDeviceSettings().temperatureUnits;
        if((temp_unit_setting == System.UNIT_METRIC and propTempUnit == 0) or propTempUnit == 1) {
            return "C";
        } else {
            return "F";
        }
    }

    hidden function formatTemperature(temp) as String {
        if(propShowTempUnit) {
            return temp.format("%d") + cachedTempUnit;
        }
        return temp.format("%d");
    }

    hidden function convertTemperature(temp as Numeric, unit as String) as Numeric {
        if(unit.equals("C")) {
            return temp;
        } else {
            return ((temp * 9/5) + 32);
        }
    }


    hidden function getWind() as String {
        var windspeed = "";
        var bearing = "";

        if(weatherCondition != null and weatherCondition.windSpeed != null) {
            var windspeed_mps = weatherCondition.windSpeed;
            if(propWindUnit == 0) { // m/s
                windspeed = Math.round(windspeed_mps).format("%01d");
            } else if (propWindUnit == 1) { // km/h
                var windspeed_kmh = Math.round(windspeed_mps * 3.6);
                windspeed = windspeed_kmh.format("%01d");
            } else if (propWindUnit == 2) { // mph
                var windspeed_mph = Math.round(windspeed_mps * 2.237);
                windspeed = windspeed_mph.format("%01d");
            } else if (propWindUnit == 3) { // knots
                var windspeed_kt = Math.round(windspeed_mps * 1.944);
                windspeed = windspeed_kt.format("%01d");
            } else if(propWindUnit == 4) { // beufort
                if (windspeed_mps < 0.5f) {
                    windspeed = "0";  // Calm
                } else if (windspeed_mps < 1.5f) {
                    windspeed = "1";  // Light air
                } else if (windspeed_mps < 3.3f) {
                    windspeed = "2";  // Light breeze
                } else if (windspeed_mps < 5.5f) {
                    windspeed = "3";  // Gentle breeze
                } else if (windspeed_mps < 7.9f) {
                    windspeed = "4";  // Moderate breeze
                } else if (windspeed_mps < 10.7f) {
                    windspeed = "5";  // Fresh breeze
                } else if (windspeed_mps < 13.8f) {
                    windspeed = "6";  // Strong breeze
                } else if (windspeed_mps < 17.1f) {
                    windspeed = "7";  // Near gale
                } else if (windspeed_mps < 20.7f) {
                    windspeed = "8";  // Gale
                } else if (windspeed_mps < 24.4f) {
                    windspeed = "9";  // Strong gale
                } else if (windspeed_mps < 28.4f) {
                    windspeed = "10";  // Storm
                } else if (windspeed_mps < 32.6f) {
                    windspeed = "11";  // Violent storm
                } else {
                    windspeed = "12";  // Hurricane force
                }
            }
        }

        if(weatherCondition != null and weatherCondition.windBearing != null) {
            bearing = ((Math.round((weatherCondition.windBearing.toFloat() + 180) / 45.0).toNumber() % 8) + 97).toChar().toString();
        }

        return bearing + windspeed;
    }

    hidden function getFeelsLike() as String {
        if(weatherCondition != null and weatherCondition.feelsLikeTemperature != null) {
            return formatTemperature(convertTemperature(weatherCondition.feelsLikeTemperature, cachedTempUnit));
        }
        return "";
    }

    hidden function getHumidity() as String {
        var ret = "";
        if(weatherCondition != null and weatherCondition.relativeHumidity != null) {
            ret = weatherCondition.relativeHumidity.format("%d") + "%";
        }
        return ret;
    }

    hidden function getUVIndex() as String {
        var ret = "";
        if(weatherCondition != null and weatherCondition has :uvIndex and weatherCondition.uvIndex != null) {
            ret = weatherCondition.uvIndex.format("%d");
        }
        return ret;
    }

    hidden function getHighLow() as String {
        var ret = "";
        if(weatherCondition != null) {
            if(weatherCondition.highTemperature != null and weatherCondition.lowTemperature != null) {
                var high = convertTemperature(weatherCondition.highTemperature, cachedTempUnit);
                var low = convertTemperature(weatherCondition.lowTemperature, cachedTempUnit);
                ret = formatTemperature(high) + "/" + formatTemperature(low);
            }
        }
        return ret;
    }

    hidden function getPrecip() as String {
        var ret = "";
        if(weatherCondition != null and weatherCondition.precipitationChance != null) {
            ret = weatherCondition.precipitationChance.format("%d") + "%";
        }
        return ret;
    }

    hidden function hoursToNextSunEvent() as String {
        var nextSunEventArray = getNextSunEvent();
        if(nextSunEventArray != null && nextSunEventArray.size() == 2) {
            var nextSunEvent = nextSunEventArray[0] as Time.Moment;
            var now = Time.now();
            // Converting seconds to hours
            var diff = (nextSunEvent.subtract(now)).value();
            if(diff >= 36000) { // No decimals if 10+ hours
                return (diff / 3600.0).format("%d");
            }
            return (diff / 3600.0).format("%.1f");
        }
        return "";
    }

    hidden function formatSunTime(s as Time.Moment?, width as Number) as String {
        if(s != null) {
            var info = Time.Gregorian.info(s, Time.FORMAT_SHORT);
            var h = formatHour(info.hour);
            if(width < 5) { return h.format("%02d") + info.min.format("%02d"); }
            return h.format("%02d") + ":" + info.min.format("%02d");
        }
        return Application.loadResource(Rez.Strings.LABEL_NA);
    }

    hidden function getNextSunEvent() as Array {
        var now = Time.now();
        if (weatherCondition != null) {
            var loc = weatherCondition.observationLocationPosition;
            if (loc != null) {
                var nextSunEvent = null;
                var sunrise = Weather.getSunrise(loc, now);
                var sunset = Weather.getSunset(loc, now);
                var isNight = false;

                if ((sunrise != null) && (sunset != null)) {
                    if (sunrise.lessThan(now)) { 
                        //if sunrise was already, take tomorrows
                        sunrise = Weather.getSunrise(loc, Time.today().add(new Time.Duration(86401)));
                    }
                    if (sunset.lessThan(now)) { 
                        //if sunset was already, take tomorrows
                        sunset = Weather.getSunset(loc, Time.today().add(new Time.Duration(86401)));
                    }
                    if (sunrise.lessThan(sunset)) { 
                        nextSunEvent = sunrise;
                        isNight = true;
                    } else {
                        nextSunEvent = sunset;
                        isNight = false;
                    }
                    return [nextSunEvent, isNight];
                }
                
            }
        }
        return [];
    }

    // Returns [dawn, dusk] as Time.Moment objects, or null if unavailable.
    // dawn = civil dawn (sun at -6°), dusk = civil dusk (sun at -6°).
    // Requires: lat_deg (latitude in degrees), sunrise and sunset as Time.Moment.
    hidden function getCivilTwilight(lat_deg as Double, sunrise as Time.Moment, sunset as Time.Moment) as Array? {
        var PI = Math.PI;
        var lat = lat_deg * PI / 180.0;

        // Half-day length as hour angle in radians (Earth rotates 2π in 86400s)
        var half_day_s = (sunset.value() - sunrise.value()) / 2.0;
        var H0 = half_day_s / 86400.0 * 2.0 * PI;

        // Back-calculate solar declination from H0 and latitude.
        // sunrise formula: cos(H0) = (sin(h0) - sin(lat)*sin(dec)) / (cos(lat)*cos(dec))
        // where h0 = -0.8333° (includes atmospheric refraction + solar disc)
        var sin_h0 = Math.sin(-0.8333 * PI / 180.0);
        var a = Math.cos(H0) * Math.cos(lat);
        var b = Math.sin(lat);
        var R = Math.sqrt(a * a + b * b);
        var ratio = sin_h0 / R;
        if (ratio < -1.0 || ratio > 1.0) { return null; }
        var alpha = Math.atan2(b, a);
        var dec = alpha - Math.acos(ratio); // valid root; other root is always ~180°+

        // Hour angle for civil twilight (sun at -6°)
        var cos_H_civil = (Math.sin(-6.0 * PI / 180.0) - Math.sin(lat) * Math.sin(dec)) /
                          (Math.cos(lat) * Math.cos(dec));
        if (cos_H_civil > 1.0) { return null; } // polar twilight — sun never drops below -6°
        if (cos_H_civil < -1.0) { return null; } // shouldn't happen when sunrise is valid
        var H_civil = Math.acos(cos_H_civil);

        var delta_s = (H_civil - H0) / (2.0 * PI) * 86400.0;
        var delta = new Time.Duration(delta_s.toNumber());
        return [sunrise.subtract(delta), sunset.add(delta)];
    }

    hidden function getRestCalories() as Number {
        var today = Time.Gregorian.info(Time.now(), Time.FORMAT_SHORT);
        var profile = UserProfile.getProfile();

        if (profile.birthYear == null || profile.weight == null || profile.height == null) {
            return 0;
        }

        var age = today.year - profile.birthYear;
        var weight = profile.weight / 1000.0;
        var rest_calories = 0;

        if (profile.gender == UserProfile.GENDER_MALE) {
            rest_calories = 5.2 - 6.116 * age + 7.628 * profile.height + 12.2 * weight;
        } else {
            rest_calories = -197.6 - 6.116 * age + 7.628 * profile.height + 12.2 * weight;
        }

        // Calculate rest calories for the current time of day
        rest_calories = Math.round((today.hour * 60 + today.min) * rest_calories / 1440).toNumber();
        return rest_calories;
    }

    hidden function getWeeklyDistance() as Number {
        var weekly_distance = 0;
        var history = ActivityMonitor.getHistory();
        if (history != null) {
            // Only take up to 6 previous days from history
            var daysToCount = history.size() < 6 ? history.size() : 6;
            for (var i = 0; i < daysToCount; i++) {
                if (history[i].distance != null) {
                    weekly_distance += history[i].distance;
                }
            }
        }
        // Add today's distance
        if(ActivityMonitor.getInfo().distance != null) {
            weekly_distance += ActivityMonitor.getInfo().distance;
        }
        return weekly_distance;
    }

    hidden function updateActivityDistCache() as Void {
        var sevenDaysAgoVal = Time.now().value() - (7 * 24 * 3600);
        var runDist = 0;
        var bikeDist = 0;
        var swimDist = 0;
        var iter = UserProfile.getUserActivityHistory();
        var act = iter.next();
        while (act != null) {
            if (act.startTime != null && act.startTime.value() >= sevenDaysAgoVal && act.distance != null) {
                if (act.type == Activity.SPORT_RUNNING) { runDist += act.distance; }
                else if (act.type == Activity.SPORT_CYCLING) { bikeDist += act.distance; }
                else if (act.type == Activity.SPORT_SWIMMING) { swimDist += act.distance; }
            }
            act = iter.next();
        }
        cachedRunDist7Days = runDist;
        cachedBikeDist7Days = bikeDist;
        cachedSwimDist7Days = swimDist;
    }

    hidden function getWeeklyDistanceFromComplication(isRun as Boolean, conversionFactor as Float, width as Number) as String {
        try {
            var compType = isRun ? Complications.COMPLICATION_TYPE_WEEKLY_RUN_DISTANCE : Complications.COMPLICATION_TYPE_WEEKLY_BIKE_DISTANCE;
            var complication = Complications.getComplication(new Id(compType));
            if (complication != null && complication.value != null) {
                return formatDistanceByWidth(complication.value * conversionFactor, width);
            }
        } catch(e) {}
        return "";
    }

    hidden function getCgmComplicationByLabel(targetLabel as String) as Complications.Id? {
        try {
            var iter = Complications.getComplications();
            var comp = iter.next();
            while (comp != null) {
                var compType = comp.getType();
                var compLabel = comp.shortLabel;
                if (compType == Complications.COMPLICATION_TYPE_INVALID && compLabel != null) {
                    if (compLabel.equals(targetLabel)) {
                        return comp.complicationId;
                    }
                }
                comp = iter.next();
            }
        } catch (e) {}
        return null;
    }

    hidden function convertCgmTrendToArrow(trend as String) as String {
        if (trend.equals("R")) { return "a"; }  // Rapidly rising ↑
        if (trend.equals("r")) { return "b"; }  // Rising ↗
        if (trend.equals("n")) { return "c"; }  // Neutral →
        if (trend.equals("d")) { return "d"; }  // Falling ↘
        if (trend.equals("D")) { return "e"; }  // Rapidly falling ↓
        return "";
    }

    hidden function getCgmReading() as String {
        try {
            if (cgmComplicationId == null) {
                cgmComplicationId = getCgmComplicationByLabel("CGM");
            }
            if (cgmComplicationId == null) { return ""; }

            var comp = Complications.getComplication(cgmComplicationId);
            if (comp == null || comp.value == null) { return ""; }

            var valueStr = comp.value.toString();
            if (valueStr.equals("---")) { return "---"; }

            var spaceIndex = valueStr.find(" ");
            if (spaceIndex == null) { return valueStr; }

            var reading = valueStr.substring(0, spaceIndex);
            var trend = valueStr.substring(spaceIndex + 1, valueStr.length());
            var arrow = convertCgmTrendToArrow(trend);
            return reading + arrow;
        } catch (e) {}
        return "";
    }
    hidden function getCgmAge() as String {
        try {
            if (cgmAgeComplicationId == null) {
                cgmAgeComplicationId = getCgmComplicationByLabel("CGM Age");
            }
            if (cgmAgeComplicationId == null) { return ""; }
            var comp = Complications.getComplication(cgmAgeComplicationId);
            if (comp == null || comp.value == null) { return ""; }
            var timestamp = comp.value.toString().toLong();
            if (timestamp == null || timestamp < 0) { return "---"; }
            var ageMin = (Time.now().value() - timestamp) / 60;
            if (ageMin < 0) { return "---"; }
            return ageMin.format("%d");
        } catch (e) {}
        return "";
    }
    hidden function secondaryTimezone(offset, width) as String {
        var val = "";
        var now = Time.now();
        var utc = Time.Gregorian.utcInfo(now, Time.FORMAT_MEDIUM);
        var min = utc.min + (offset % 60);
        var hour = (utc.hour + Math.floor(offset / 60)) % 24;

        if(min > 59) {
            min -= 60;
            hour += 1;
        }

        if(min < 0) {
            min += 60;
            hour -= 1;
        }

        if(hour < 0) {
            hour += 24;
        }
        if(hour > 23) {
            hour -= 24;
        }
        var f_hour = formatHour(hour);
        if(width < 5) {
            val = f_hour.format("%02d") + min.format("%02d");
        } else {
            if(propTimeSeparator == 3) {
                var ampm = "A";
                if(hour >= 12) { ampm = "P"; }
                val = f_hour.format("%02d") + min.format("%02d") + ampm;
            } else {
                val = f_hour.format("%02d") + ":" + min.format("%02d");
            }
        }
        return val;
    }

    hidden function dayName(day_of_week as Number) as String {
        if (cachedDayOfWeek == day_of_week) { return cachedDayName; }
        cachedDayOfWeek = day_of_week;
        var names = [Rez.Strings.DAY_OF_WEEK_SUN, Rez.Strings.DAY_OF_WEEK_MON, Rez.Strings.DAY_OF_WEEK_TUE,
                     Rez.Strings.DAY_OF_WEEK_WED, Rez.Strings.DAY_OF_WEEK_THU, Rez.Strings.DAY_OF_WEEK_FRI,
                     Rez.Strings.DAY_OF_WEEK_SAT];
        cachedDayName = Application.loadResource(names[day_of_week - 1]);
        return cachedDayName;
    }

    hidden function monthName(month as Number) as String {
        if (cachedMonth == month) { return cachedMonthName; }
        cachedMonth = month;
        var names = [Rez.Strings.MONTH_JAN, Rez.Strings.MONTH_FEB, Rez.Strings.MONTH_MAR,
                     Rez.Strings.MONTH_APR, Rez.Strings.MONTH_MAY, Rez.Strings.MONTH_JUN,
                     Rez.Strings.MONTH_JUL, Rez.Strings.MONTH_AUG, Rez.Strings.MONTH_SEP,
                     Rez.Strings.MONTH_OCT, Rez.Strings.MONTH_NOV, Rez.Strings.MONTH_DEC];
        cachedMonthName = Application.loadResource(names[month - 1]);
        return cachedMonthName;
    }

    hidden function isoWeekNumber(year as Number, month as Number, day as Number) as Number {
        var first_day_of_year = julianDay(year, 1, 1);
        var given_day_of_year = julianDay(year, month, day);
        var day_of_week = (first_day_of_year + 3) % 7;
        var week_of_year = (given_day_of_year - first_day_of_year + day_of_week + 4) / 7;
        var ret = 0;
        if (week_of_year == 53) {
            if (day_of_week == 6) {
                ret = week_of_year;
            } else if (day_of_week == 5 && isLeapYear(year)) {
                ret = week_of_year;
            } else {
                ret = 1;
            }
        } else if (week_of_year == 0) {
            first_day_of_year = julianDay(year - 1, 1, 1);
            day_of_week = (first_day_of_year + 3) % 7;
            ret = (given_day_of_year - first_day_of_year + day_of_week + 4) / 7;
        } else {
            ret = week_of_year;
        }
        if(propWeekOffset != 0) {
            ret = ret + propWeekOffset;
        }
        return ret;
    }

    hidden function julianDay(year as Number, month as Number, day as Number) as Number {
        var a = (14 - month) / 12;
        var y = (year + 4800 - a);
        var m = (month + 12 * a - 3);
        return day + ((153 * m + 2) / 5) + (365 * y) + (y / 4) - (y / 100) + (y / 400) - 32045;
    }

    hidden function isLeapYear(year as Number) as Boolean {
        if (year % 4 != 0) {
            return false;
           } else if (year % 100 != 0) {
            return true;
        } else if (year % 400 == 0) {
            return true;
        }
        return false;
    }

    // Square helper functions - only compiled for square devices
    (:Square)
    hidden function loadBottomField2Property() as Void {
        propBottomField2Shows = Application.Properties.getValue("bottomField2Shows") as Number;
    }

    (:Square)
    hidden function computeBottomField2Values(values as Dictionary) as Void {
        values[:dataBottom2] = getValueByType(propBottomField2Shows, 5);
        if (propBottomFieldShows != -2 and propBottomField2Shows != -2) {
            values[:dataLabelBottom] = getLabelByType(propBottomFieldShows, 2);
            values[:dataLabelBottom2] = getLabelByType(propBottomField2Shows, 2);
        }
    }

    (:Square)
    hidden function calculateSquareLayout() as Void {
        dualBottomFieldActive = (propBottomFieldShows != -2 and propBottomField2Shows != -2);
        bottomFiveYOriginal = bottomFiveY;

        if (dualBottomFieldActive) {
            // Position two 5-digit fields with 40px gap between them, centered
            var fieldWidth = bottomDataWidth * 5;
            var gap = 20;

            bottomFive1X = centerX - (gap / 2) - (fieldWidth / 2);
            bottomFive2X = centerX + (gap / 2) + (fieldWidth / 2);

            // Shift the entire row DOWN to make room for labels above (only if labels visible)
            if (propLabelVisibility == 0 or propLabelVisibility == 2) {
                bottomFiveY = bottomFiveY + labelHeight + labelMargin;
            }
        } else {
            // Single field mode - center position
            bottomFive1X = centerX;
            bottomFive2X = centerX;
        }
    }

    // Non-Square stubs for other devices
    (:Round)
    hidden function calculateSquareLayout() as Void {
        // No-op for non-square devices
    }

    (:Round)
    hidden function loadBottomField2Property() as Void {
        // No-op for non-square devices devices
    }

    (:Round)
    hidden function computeBottomField2Values(values as Dictionary) as Void {
        // No-op for non-square devices devices
    }

    (:Square)
    hidden function drawBottomFieldsWithIcons(dc as Dc, values as Dictionary) as Void {
        if (dualBottomFieldActive) {
            var field1Width = bottomDataWidth * 5;
            var field2Width = bottomDataWidth * 5;
            var field1Left = bottomFive1X - (field1Width / 2);
            var field2Left = bottomFive2X - (field2Width / 2);

            // Draw labels above fields - left aligned with field edge
            if (propLabelVisibility == 0 or propLabelVisibility == 2) {
                dc.setColor(themeColors[fieldLbl], Graphics.COLOR_TRANSPARENT);
                dc.drawText(field1Left, bottomFiveYOriginal, fontLabel, values[:dataLabelBottom], Graphics.TEXT_JUSTIFY_LEFT);
                dc.drawText(field2Left, bottomFiveYOriginal, fontLabel, values[:dataLabelBottom2], Graphics.TEXT_JUSTIFY_LEFT);
            }

            // Draw both fields
            drawDataField(dc, bottomFive1X, bottomFiveY, 3,
                null, values[:dataBottom], 5,
                fontBottomData, field1Width);

            drawDataField(dc, bottomFive2X, bottomFiveY, 3,
                null, values[:dataBottom2], 5,
                fontBottomData, field2Width);

            // Icons on outer edges
            dc.setColor(themeColors[dataVal], Graphics.COLOR_TRANSPARENT);
            dc.drawText(field1Left - (marginX / 2),
                bottomFiveY + (largeDataHeight / 2) + iconYAdj,
                fontIcons, values[:dataIcon1],
                Graphics.TEXT_JUSTIFY_RIGHT | Graphics.TEXT_JUSTIFY_VCENTER);
            dc.drawText(field2Left + field2Width + (marginX / 2) - 2,
                bottomFiveY + (largeDataHeight / 2) + iconYAdj,
                fontIcons, values[:dataIcon2],
                Graphics.TEXT_JUSTIFY_LEFT | Graphics.TEXT_JUSTIFY_VCENTER);
        } else {
            // Single field - original behavior
            var step_width = drawDataField(dc, centerX, bottomFiveY, 3, null, values[:dataBottom], 5, fontBottomData, bottomDataWidth * 5);

            // Draw icons
            dc.setColor(themeColors[dataVal], Graphics.COLOR_TRANSPARENT);
            if(propFontSize == 1 and step_width == 0) {
                var y = 0;
                if(screenWidth <= 280) {
                    step_width = 45;
                    y = screenHeight - 28;
                } else {
                    step_width = 65;
                    y = screenHeight - 31;
                }
                dc.drawText(centerX - (step_width / 2) - (marginX / 2), y, fontIcons, values[:dataIcon1], Graphics.TEXT_JUSTIFY_RIGHT);
                dc.drawText(centerX + (step_width / 2) + (marginX / 2) - 2, y, fontIcons, values[:dataIcon2], Graphics.TEXT_JUSTIFY_LEFT);
            } else {
                dc.drawText(centerX - (step_width / 2) - (marginX / 2), bottomFiveY + (largeDataHeight / 2) + iconYAdj, fontIcons, values[:dataIcon1], Graphics.TEXT_JUSTIFY_RIGHT | Graphics.TEXT_JUSTIFY_VCENTER);
                dc.drawText(centerX + (step_width / 2) + (marginX / 2) - 2, bottomFiveY + (largeDataHeight / 2) + iconYAdj, fontIcons, values[:dataIcon2], Graphics.TEXT_JUSTIFY_LEFT | Graphics.TEXT_JUSTIFY_VCENTER);
            }
        }
    }

    (:Round)
    hidden function drawBottomFieldsWithIcons(dc as Dc, values as Dictionary) as Void {
        var step_width = drawDataField(dc, centerX, bottomFiveY, 3, null, values[:dataBottom], 5, fontBottomData, bottomDataWidth * 5);

        // Draw icons
        dc.setColor(themeColors[dataVal], Graphics.COLOR_TRANSPARENT);
        if(propFontSize == 1 and step_width == 0) {
            var y = 0;
            if(screenWidth <= 280) {
                step_width = 45;
                y = screenHeight - 28;
            } else {
                step_width = 65;
                y = screenHeight - 31;
            }
            dc.drawText(centerX - (step_width / 2) - (marginX / 2), y, fontIcons, values[:dataIcon1], Graphics.TEXT_JUSTIFY_RIGHT);
            dc.drawText(centerX + (step_width / 2) + (marginX / 2) - 2, y, fontIcons, values[:dataIcon2], Graphics.TEXT_JUSTIFY_LEFT);
        } else {
            dc.drawText(centerX - (step_width / 2) - (marginX / 2), bottomFiveY + (largeDataHeight / 2) + iconYAdj, fontIcons, values[:dataIcon1], Graphics.TEXT_JUSTIFY_RIGHT | Graphics.TEXT_JUSTIFY_VCENTER);
            dc.drawText(centerX + (step_width / 2) + (marginX / 2) - 2, bottomFiveY + (largeDataHeight / 2) + iconYAdj, fontIcons, values[:dataIcon2], Graphics.TEXT_JUSTIFY_LEFT | Graphics.TEXT_JUSTIFY_VCENTER);
        }
    }

}

(:background_excluded)
class Segment34Delegate extends WatchUi.WatchFaceDelegate {
    var screenW = null;
    var screenH = null;
    var view as Segment34View;

    public function initialize(v as Segment34View) {
        WatchFaceDelegate.initialize();
        screenW = System.getDeviceSettings().screenWidth;
        screenH = System.getDeviceSettings().screenHeight;
        view = v;
    }

    public function onPress(clickEvent as WatchUi.ClickEvent) {
        var coords = clickEvent.getCoordinates();
        var x = coords[0];
        var y = coords[1];

        if(y < screenH / 3) {
            handlePress("pressToOpenTop");
        } else if (y < (screenH / 3) * 2) {
            handlePress("pressToOpenMiddle");
        } else if (x < screenW / 3) {
            handlePress("pressToOpenBottomLeft");
        } else if (x < (screenW / 3) * 2) {
            handlePress("pressToOpenBottomCenter");
        } else {
            handlePress("pressToOpenBottomRight");
        }

        return true;
    }

    function handlePress(areaSetting as String) {
        var cID = Application.Properties.getValue(areaSetting) as Complications.Type;

        if(cID == -1) {
            switch(view.nightModeOverride) {
                case 1:
                    view.nightModeOverride = 0;
                    view.infoMessage = "DAY THEME";
                    break;
                case 0:
                    view.nightModeOverride = -1;
                    view.infoMessage = "THEME AUTO";
                    break;
                default:
                    view.nightModeOverride = 1;
                    view.infoMessage = "NIGHT THEME";
            }
            view.onSettingsChanged();
        }

        if(cID != null and cID > 0) {
            try {
                Complications.exitTo(new Id(cID));
            } catch (e) {}
        }
    }

}

(:background_excluded)
class StoredWeather {
    public var observationLocationPosition as Position.Location or Null;
    public var precipitationChance as Lang.Number or Null;
    public var temperature as Lang.Numeric or Null;
    public var windBearing as Lang.Number or Null;
    public var windSpeed as Lang.Float or Null;
    public var highTemperature as Lang.Numeric or Null;
    public var lowTemperature as Lang.Numeric or Null;
    public var feelsLikeTemperature as Lang.Float or Null;
    public var relativeHumidity as Lang.Number or Null;
    public var condition as Lang.Number or Null;
    public var uvIndex as Lang.Float or Null;
    public var cityName as Lang.String or Null;
}